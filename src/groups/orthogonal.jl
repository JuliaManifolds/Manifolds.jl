@doc raw"""
    Orthogonal{n} <: GroupManifold{â„,OrthogonalMatrices{n},MultiplicationOperation}

Orthogonal group $\mathrm{O}(n)$ represented by orthogonal matrices.

# Constructor
    SpecialOrthogonal(n)
"""
const Orthogonal{n} = AbstractUnitaryMultiplicationGroup{n,â„,OrthogonalMatrices{n}}

Orthogonal(n) = Orthogonal{n}()

@doc raw"""
     get_coordinates(G::Orthogonal, p, X)
 Extract the unique tangent vector components $X^i$ at point `p` on [`Orthogonal`](@ref)
 $\mathrm{O}(n)$ from the matrix representation `X` of the tangent vector.
 The basis on the Lie algebra $ð”¬(n)$ is chosen such that
 for $\mathrm{O}(2)$, $X^1 = Î¸ = X_{21}$ is the angle of rotation, and
 for $\mathrm{O}(3)$, $(X^1, X^2, X^3) = (X_{32}, X_{13}, X_{21}) = Î¸ u$ is the
 angular velocity and axis-angle representation, where $u$ is the unit vector
 along the axis of rotation.
 For $\mathrm{O}(n)$ where $n â‰¥ 4$, the additional elements of $X^i$ are
 $X^{j (j - 3)/2 + k + 1} = X_{jk}$, for $j âˆˆ [4,n], k âˆˆ [1,j)$.
 """
 get_coordinates(::Orthogonal, ::Any...)
 get_coordinates(::Orthogonal{2}, p, X, ::DefaultOrthogonalBasis) = [X[2]]

 function get_coordinates!(::Orthogonal{2}, Xâ±, p, X, ::DefaultOrthogonalBasis)
     Xâ±[1] = X[2]
     return Xâ±
 end
 function get_coordinates!(G::Orthogonal{n}, Xâ±, p, X, B::DefaultOrthogonalBasis) where {n}
     @inbounds begin
         Xâ±[1] = X[3, 2]
         Xâ±[2] = X[1, 3]
         Xâ±[3] = X[2, 1]

         k = 4
         for i in 4:n, j in 1:(i - 1)
             Xâ±[k] = X[i, j]
             k += 1
         end
     end
     return Xâ±
 end
 function get_coordinates!(G::Orthogonal, Xâ±, p, X, ::DefaultOrthonormalBasis)
     get_coordinates!(G, Xâ±, p, X, DefaultOrthogonalBasis())
     T = eltype(Xâ±)
     Xâ± .*= sqrt(T(2))
     return Xâ±
 end

 @doc raw"""
     get_vector(G::Orthogonal, p, Xâ±, B:: DefaultOrthogonalBasis)
 Convert the unique tangent vector components `Xâ±` at point `p` on [`Orthogonal`](@ref)
 group $\mathrm{O}(n)$ to the matrix representation $X$ of the tangent vector. See
 [`get_coordinates`](@ref get_coordinates(::Orthogonal, ::Any...)) for the conventions used.
 """
 get_vector(::Orthogonal, ::Any...)

 function get_vector!(G::Orthogonal{2}, X, p, Xâ±, B::DefaultOrthogonalBasis)
     return get_vector!(G, X, p, Xâ±[1], B)
 end
 function get_vector!(::Orthogonal{2}, X, p, Xâ±::Real, ::DefaultOrthogonalBasis)
     @assert size(X) == (2, 2)
     @inbounds begin
         X[1, 1] = X[2, 2] = 0
         X[2, 1] = Xâ±
         X[1, 2] = -Xâ±
     end
     return X
 end
 function get_vector!(G::Orthogonal{n}, X, p, Xâ±, ::DefaultOrthogonalBasis) where {n}
     @assert size(X) == (n, n)
     @assert length(Xâ±) == manifold_dimension(G)
     @inbounds begin
         X[1, 1] = 0
         X[1, 2] = -Xâ±[3]
         X[1, 3] = Xâ±[2]
         X[2, 1] = Xâ±[3]
         X[2, 2] = 0
         X[2, 3] = -Xâ±[1]
         X[3, 1] = -Xâ±[2]
         X[3, 2] = Xâ±[1]
         X[3, 3] = 0
         k = 4
         for i in 4:n
             for j in 1:(i - 1)
                 X[i, j] = Xâ±[k]
                 X[j, i] = -Xâ±[k]
                 k += 1
             end
             X[i, i] = 0
         end
     end
     return X
 end
 function get_vector!(G::Orthogonal, X, p, Xâ±, B::DefaultOrthonormalBasis)
     get_vector!(G, X, p, Xâ±, DefaultOrthogonalBasis())
     T = eltype(X)
     X .*= inv(sqrt(T(2)))
     return X
 end