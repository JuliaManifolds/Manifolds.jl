@doc raw"""
    GeneralUnitaryMultiplicationGroup{n,ğ”½,M} = GroupManifold{ğ”½,M,MultiplicationOperation}

A generic type for Lie groups based on a unitary property and matrix multiplcation,
see e.g. [`Orthogonal`](@ref), [`SpecialOrthogonal`](@ref), [`Unitary`](@ref), and [`SpecialUnitary`](@ref)
"""
struct GeneralUnitaryMultiplicationGroup{n,ğ”½,S} <: AbstractDecoratorManifold{ğ”½}
    manifold::GeneralUnitaryMatrices{n,ğ”½,S}
end

@inline function active_traits(f, ::GeneralUnitaryMultiplicationGroup, args...)
    if is_metric_function(f)
        #pass to Rotations by default - but keep Group Decorator for the retraction
        return merge_traits(
            IsGroupManifold(MultiplicationOperation()),
            IsExplicitDecorator(),
        )
    else
        return merge_traits(
            IsGroupManifold(MultiplicationOperation()),
            HasBiinvariantMetric(),
            IsDefaultMetric(EuclideanMetric()),
            IsExplicitDecorator(), #pass to the inner M by default/last fallback
        )
    end
end

function allocate_result(
    ::GeneralUnitaryMultiplicationGroup,
    ::typeof(exp),
    ::Identity{MultiplicationOperation},
    X,
)
    return allocate(X)
end
function allocate_result(
    ::GeneralUnitaryMultiplicationGroup,
    ::typeof(log),
    ::Identity{MultiplicationOperation},
    q,
)
    return allocate(q)
end

decorated_manifold(G::GeneralUnitaryMultiplicationGroup) = G.manifold

@doc raw"""
     exp_lie(G::Orthogonal{2}, X)
     exp_lie(G::SpecialOrthogonal{2}, X)

Compute the Lie group exponential map on the [`Orthogonal`](@ref)`(2)` or [`SpecialOrthogonal`](@ref)`(2)` group.
Given ``X = \begin{pmatrix} 0 & -Î¸ \\ Î¸ & 0 \end{pmatrix}``, the group exponential is

```math
\exp_e \colon X â†¦ \begin{pmatrix} \cos Î¸ & -\sin Î¸ \\ \sin Î¸ & \cos Î¸ \end{pmatrix}.
```
"""
exp_lie(::GeneralUnitaryMultiplicationGroup{2,â„}, X)

@doc raw"""
     exp_lie(G::Orthogonal{4}, X)
     exp_lie(G::SpecialOrthogonal{4}, X)

Compute the group exponential map on the [`Orthogonal`](@ref)`(4)` or the [`SpecialOrthogonal`](@ref) group.
The algorithm used is a more numerically stable form of those proposed in [^Gallier2002], [^Andrica2013].

[^Gallier2002]:
    > Gallier J.; Xu D.; Computing exponentials of skew-symmetric matrices
    > and logarithms of orthogonal matrices.
    > International Journal of Robotics and Automation (2002), 17(4), pp. 1-11.
    > [pdf](http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.35.3205).
[^Andrica2013]:
    > Andrica D.; Rohan R.-A.; Computing the Rodrigues coefficients of the
    > exponential map of the Lie groups of matrices.
    > Balkan Journal of Geometry and Its Applications (2013), 18(2), pp. 1-2.
    > [pdf](https://www.emis.de/journals/BJGA/v18n2/B18-2-an.pdf).
"""
exp_lie(::GeneralUnitaryMultiplicationGroup{4,â„}, X)

function exp_lie!(::GeneralUnitaryMultiplicationGroup{2,â„}, q, X)
    @assert size(X) == (2, 2)
    @inbounds Î¸ = (X[2, 1] - X[1, 2]) / 2
    sinÎ¸, cosÎ¸ = sincos(Î¸)
    @inbounds begin
        q[1, 1] = cosÎ¸
        q[2, 1] = sinÎ¸
        q[1, 2] = -sinÎ¸
        q[2, 2] = cosÎ¸
    end
    return q
end
function exp_lie!(::GeneralUnitaryMultiplicationGroup{3,â„}, q, X)
    Î¸ = norm(X) / sqrt(2)
    if Î¸ â‰ˆ 0
        a = 1 - Î¸^2 / 6
        b = Î¸ / 2
    else
        a = sin(Î¸) / Î¸
        b = (1 - cos(Î¸)) / Î¸^2
    end
    copyto!(q, I)
    q .+= a .* X
    mul!(q, X, X, b, true)
    return q
end
function exp_lie!(::GeneralUnitaryMultiplicationGroup{4,â„}, q, X)
    T = eltype(X)
    Î±, Î² = angles_4d_skew_sym_matrix(X)
    sinÎ±, cosÎ± = sincos(Î±)
    sinÎ², cosÎ² = sincos(Î²)
    Î±Â² = Î±^2
    Î²Â² = Î²^2
    Î” = Î²Â² - Î±Â²
    if !isapprox(Î”, 0; atol=1e-6)  # Case Î± > Î² â‰¥ 0
        sincÎ± = sinÎ± / Î±
        sincÎ² = Î² == 0 ? one(T) : sinÎ² / Î²
        aâ‚€ = (Î²Â² * cosÎ± - Î±Â² * cosÎ²) / Î”
        aâ‚ = (Î²Â² * sincÎ± - Î±Â² * sincÎ²) / Î”
        aâ‚‚ = (cosÎ± - cosÎ²) / Î”
        aâ‚ƒ = (sincÎ± - sincÎ²) / Î”
    elseif Î± == 0 # Case Î± = Î² = 0
        aâ‚€ = aâ‚ = one(T)
        aâ‚‚ = inv(T(2))
        aâ‚ƒ = inv(T(6))
    else  # Case Î± âª† Î² â‰¥ 0, Î± â‰  0
        sincÎ± = sinÎ± / Î±
        r = Î² / Î±
        c = 1 / (1 + r)
        d = Î± * (Î± - Î²) / 2
        if Î± < 1e-2
            e = evalpoly(Î±Â², (inv(T(3)), inv(T(-30)), inv(T(840)), inv(T(-45360))))
        else
            e = (sincÎ± - cosÎ±) / Î±Â²
        end
        aâ‚€ = (Î± * sinÎ± + (1 + r - d) * cosÎ±) * c
        aâ‚ = ((3 - d) * sincÎ± - (2 - r) * cosÎ±) * c
        aâ‚‚ = (sincÎ± - (1 - r) / 2 * cosÎ±) * c
        aâ‚ƒ = (e + (1 - r) * (e - sincÎ± / 2)) * c
    end

    XÂ² = X * X
    XÂ³ = XÂ² * X
    q = aâ‚€ * I + aâ‚ .* X .+ aâ‚‚ .* XÂ² .+ aâ‚ƒ .* XÂ³
    return q
end

inverse_translate(G::GeneralUnitaryMultiplicationGroup, p, q, ::LeftAction) = inv(G, p) * q
inverse_translate(G::GeneralUnitaryMultiplicationGroup, p, q, ::RightAction) = q * inv(G, p)

function inverse_translate!(G::GeneralUnitaryMultiplicationGroup, x, p, q, ::LeftAction)
    return mul!(x, inv(G, p), q)
end
function inverse_translate!(G::GeneralUnitaryMultiplicationGroup, x, p, q, ::RightAction)
    return mul!(x, q, inv(G, p))
end

function inverse_translate_diff(
    G::GeneralUnitaryMultiplicationGroup,
    p,
    q,
    X,
    conv::ActionDirection,
)
    return translate_diff(G, inv(G, p), q, X, conv)
end
function inverse_translate_diff!(
    G::GeneralUnitaryMultiplicationGroup,
    Y,
    p,
    q,
    X,
    conv::ActionDirection,
)
    return copyto!(Y, inverse_translate_diff(G, p, q, X, conv))
end

function log_lie!(
    G::GeneralUnitaryMultiplicationGroup{n,â„},
    X::AbstractMatrix,
    q::AbstractMatrix,
) where {n}
    log_safe!(X, q)
    return project!(G, X, Identity(G), X)
end
function log_lie!(
    ::GeneralUnitaryMultiplicationGroup{n,â„},
    X,
    ::Identity{MultiplicationOperation},
) where {n}
    fill!(X, 0)
    return X
end
function log_lie!(
    G::GeneralUnitaryMultiplicationGroup{2,â„},
    X::AbstractMatrix,
    q::AbstractMatrix,
)
    @assert size(q) == (2, 2)
    @inbounds Î¸ = atan(q[2, 1], q[1, 1])
    return get_vector!(G, X, Identity(G), Î¸, DefaultOrthogonalBasis())
end
function log_lie!(
    G::GeneralUnitaryMultiplicationGroup{3,â„},
    X::AbstractMatrix,
    q::AbstractMatrix,
)
    e = Identity(G)
    cosÎ¸ = (tr(q) - 1) / 2
    if cosÎ¸ â‰ˆ -1
        eig = eigen_safe(q)
        ival = findfirst(Î» -> isapprox(Î», 1), eig.values)
        inds = SVector{3}(1:3)
        ax = eig.vectors[inds, ival]
        return get_vector!(G, X, e, Ï€ * ax, DefaultOrthogonalBasis())
    end
    X .= q ./ usinc_from_cos(cosÎ¸)
    return project!(G, X, e, X)
end
function log_lie!(
    G::GeneralUnitaryMultiplicationGroup{4,â„},
    X::AbstractMatrix,
    q::AbstractMatrix,
)
    cosÎ±, cosÎ² = cos_angles_4d_rotation_matrix(q)
    Î± = acos(clamp(cosÎ±, -1, 1))
    Î² = acos(clamp(cosÎ², -1, 1))
    if Î± â‰ˆ 0 && Î² â‰ˆ Ï€
        AÂ² = Symmetric((q - I) ./ 2)
        P = eigvecs(AÂ²)
        E = similar(q)
        fill!(E, 0)
        @inbounds begin
            E[2, 1] = -Î²
            E[1, 2] = Î²
        end
        copyto!(X, P * E * transpose(P))
    else
        det(q) < 0 && throw(
            DomainError(
                "The Lie group logarithm is not defined for $q with a negative determinant ($(det(q)) < 0). Point `q` is in a different connected component of the manifold $G",
            ),
        )
        log_safe!(X, q)
    end
    return project!(G, X, Identity(G), X)
end

function Random.rand!(G::GeneralUnitaryMultiplicationGroup, pX; kwargs...)
    rand!(G.manifold, pX; kwargs...)
    return pX
end
function Random.rand!(rng::AbstractRNG, G::GeneralUnitaryMultiplicationGroup, pX; kwargs...)
    rand!(rng, G.manifold, pX; kwargs...)
    return pX
end

function translate_diff!(G::GeneralUnitaryMultiplicationGroup, Y, p, q, X, ::LeftAction)
    return copyto!(G, Y, p, X)
end
function translate_diff!(G::GeneralUnitaryMultiplicationGroup, Y, p, q, X, ::RightAction)
    return copyto!(G, Y, p, inv(G, p) * X * p)
end
