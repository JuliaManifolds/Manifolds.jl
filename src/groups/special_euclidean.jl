@doc doc"""
    SpecialEuclidean(n)

Special Euclidean group $\mathrm{SE}(n)$, the group of rigid motions.

``\mathrm{SE}(n)`` is the semidirect product of the [`TranslationGroup`](@ref) on $â„^n$ and
[`SpecialOrthogonal(n)`](@ref)

````math
\mathrm{SE}(n) â‰ \mathrm{T}(n) â‹Š_Î¸ \mathrm{SO}(n),
````

where $Î¸$ is the canonical action of $\mathrm{SO}(n)$ on $\mathrm{T}(n)$ by vector rotation.

This constructor is equivalent to calling

```julia
Tn = TranslationGroup(n)
SOn = SpecialOrthogonal(n)
SemidirectProductGroup(Tn, SOn, RotationAction(Tn, SOn))
```

Points on $\mathrm{SE}(n)$ may be represented as points on the underlying product manifold
$\mathrm{T}(n) \times \mathrm{SO}(n)$. For group-specific functions, they may also be
represented as affine matrices with size `(n + 1, n + 1)` (see [`affine_matrix`](@ref)), for
which the group operation is [`MultiplicationOperation`](@ref).
"""
const SpecialEuclidean{N} = SemidirectProductGroup{
    TranslationGroup{Tuple{N},â„},
    SpecialOrthogonal{N},
    RotationAction{TranslationGroup{Tuple{N},â„},SpecialOrthogonal{N},LeftAction},
}

function SpecialEuclidean(n)
    Tn = TranslationGroup(n)
    SOn = SpecialOrthogonal(n)
    A = RotationAction(Tn, SOn)
    return SemidirectProductGroup(Tn, SOn, A)
end

show(io::IO, ::SpecialEuclidean{n}) where {n} = print(io, "SpecialEuclidean($(n))")

Base.@propagate_inbounds function submanifold_component(
    ::SpecialEuclidean{n},
    x::AbstractMatrix,
    ::Val{1},
) where {n}
    return view(x, 1:n, n + 1)
end
Base.@propagate_inbounds function submanifold_component(
    ::SpecialEuclidean{n},
    x::AbstractMatrix,
    ::Val{2},
) where {n}
    return view(x, 1:n, 1:n)
end

function submanifold_components(G::SpecialEuclidean{n}, x::AbstractMatrix) where {n}
    @assert size(x) == (n + 1, n + 1)
    @inbounds t = submanifold_component(G, x, Val(1))
    @inbounds R = submanifold_component(G, x, Val(2))
    return (t, R)
end

Base.@propagate_inbounds function _padpoint!(
    ::SpecialEuclidean{n},
    y::AbstractMatrix,
) where {n}
    for i = 1:n
        y[n+1, i] = 0
    end
    y[n+1, n+1] = 1
    return y
end

Base.@propagate_inbounds function _padvector!(
    ::SpecialEuclidean{n},
    v::AbstractMatrix,
) where {n}
    for i = 1:n+1
        v[n+1, i] = 0
    end
    return v
end

@doc doc"""
    affine_matrix(G::SpecialEuclidean, x) -> AbstractMatrix

Represent the point $x âˆˆ \mathrm{SE}(n)$ as an affine matrix.
For $x = (t, R) âˆˆ \mathrm{SE}(n)$, where $t âˆˆ \mathrm{T}(n), R âˆˆ \mathrm{SO}(n)$, the
affine representation is the $n + 1 Ã— n + 1$ matrix

````math
\begin{pmatrix}
R & t \\
0^\mathrm{T} & 1
\end{pmatrix}.
````

    affine_matrix(G::SpecialEuclidean, e, v) -> AbstractMatrix

Represent the Lie algebra element $v âˆˆ ğ”°ğ”¢(n) = T_e \mathrm{SE}(n)$ as a (screw) matrix.
For $v = (b, Î©) âˆˆ ğ”°ğ”¢(n)$, where $Î© âˆˆ ğ”°ğ”¬(n) = T_e \mathrm{SO}(n)$, the screw representation is
the $n + 1 Ã— n + 1$ matrix

````math
\begin{pmatrix}
Î© & b \\
0^\mathrm{T} & 0
\end{pmatrix}.
````
"""
function affine_matrix(G::SpecialEuclidean{n}, x) where {n}
    y = allocate(x, Size(n + 1, n + 1))
    map(copyto!, submanifold_components(G, y), submanifold_components(G, x))
    @inbounds _padpoint!(G, y)
    return y
end
affine_matrix(::SpecialEuclidean{n}, x::AbstractMatrix) where {n} = x
@generated function affine_matrix(::GT, ::Identity{GT}) where {n,GT<:SpecialEuclidean{n}}
    return SDiagonal{n}(I)
end
function affine_matrix(G::SpecialEuclidean{n}, e, v) where {n}
    w = allocate(v, Size(n + 1, n + 1))
    map(copyto!, submanifold_components(G, w), submanifold_components(G, v))
    @inbounds _padvector!(G, w)
    return w
end
affine_matrix(::SpecialEuclidean{n}, e, v::AbstractMatrix) where {n} = v

compose(::SpecialEuclidean, x::AbstractMatrix, y::AbstractMatrix) = x * y

function compose!(
    ::SpecialEuclidean,
    z::AbstractMatrix,
    x::AbstractMatrix,
    y::AbstractMatrix,
)
    return mul!(z, x, y)
end

@doc doc"""
    group_exp(G::SpecialEuclidean, v)

Compute the group exponential of $v âˆˆ ğ”°ğ”¢(n)$. In the [`affine_matrix`](@ref) representation,
the group exponential is the matrix exponential (see [`group_exp`](@ref)).

    group_exp(G::SpecialEuclidean{2}, v)

The group exponential on $\mathrm{SE}(2)$ for $v = (b, Î©) âˆˆ ğ”°ğ”¢(2)$, where $R âˆˆ ğ”°ğ”¬(2)$ is

````math
\exp v = (U(Î¸) b, \exp Î©) = (t, R),
````

where $R = \exp Î©$ is the group exponential on $\mathrm{SO}(3)$, and $U(Î¸)$ is

````math
U(Î¸) = \frac{\sin Î¸}{Î¸} I_2 + \frac{1 - \cos Î¸}{Î¸^2} Î©,
````

where $Î¸ = \frac{1}{\sqrt{2}} \lVert Î© \rVert_e$
(see [`norm`](@ref norm(M::Rotations, x, v))) is the angle of the rotation.

    group_exp(G::SpecialEuclidean{3}, v)

The group exponential on $\mathrm{SE}(3)$ for $v = (b, Î©) âˆˆ ğ”°ğ”¢(3)$, where $R âˆˆ ğ”°ğ”¬(3)$ is

````math
\exp v = (U(Î¸) b, \exp Î©) = (t, R),
````

where $R = \exp Î©$ is the group exponential on $\mathrm{SO}(3)$, and $U(Î¸)$ is

````math
U(Î¸) = I_3 + \frac{1 - \cos Î¸}{Î¸^2} Î© + \frac{Î¸ - \sin Î¸}{Î¸^3} Î©^2,
````

where $Î¸$ is the same as above.
"""
group_exp(::SpecialEuclidean, ::Any)

function group_exp!(G::SpecialEuclidean, y, v)
    vmat = affine_matrix(G, Identity(G), v)
    expv = exp(vmat)
    map(copyto!, submanifold_components(G, y), submanifold_components(G, expv))
    _padpoint!(G, y)
    return y
end
function group_exp!(G::SpecialEuclidean{2}, y, v)
    SO2 = submanifold(G, 2)
    b, Î© = submanifold_components(G, v)
    t, R = submanifold_components(G, y)
    @assert size(R) == (2, 2)
    @assert size(t) == (2,)
    @assert size(b) == (2,)

    Î¸ = vee(SO2, Identity(SO2), Î©)[1]
    sinÎ¸, cosÎ¸ = sincos(Î¸)
    if Î¸ â‰ˆ 0
        Î± = 1 - Î¸^2 / 6
        Î² = Î¸ / 2
    else
        Î± = sinÎ¸ / Î¸
        Î² = (1 - cosÎ¸) / Î¸
    end

    @inbounds begin
        R[1] = cosÎ¸
        R[2] = sinÎ¸
        R[3] = -sinÎ¸
        R[4] = cosÎ¸
        t[1] = Î± * b[1] - Î² * b[2]
        t[2] = Î± * b[2] + Î² * b[1]
        _padpoint!(G, y)
    end
    return y
end
function group_exp!(G::SpecialEuclidean{3}, y, v)
    SO3 = submanifold(G, 2)
    b, Î© = submanifold_components(G, v)
    t, R = submanifold_components(G, y)
    @assert size(R) == (3, 3)
    @assert size(t) == (3,)

    Î¸ = norm(SO3, Identity(SO3), Î©) / sqrt(2)
    Î¸Â² = Î¸^2
    if Î¸ â‰ˆ 0
        Î± = 1 - Î¸Â² / 6
        Î² = Î¸ / 2
        Î³ = 1 / 6 - Î¸Â² / 120
    else
        sinÎ¸, cosÎ¸ = sincos(Î¸)
        Î± = sinÎ¸ / Î¸
        Î² = (1 - cosÎ¸) / Î¸Â²
        Î³ = (1 - Î±) / Î¸Â²
    end

    Î©Â² = Î©^2
    Jâ‚— = I + Î² .* Î© .+ Î³ .* Î©Â²
    R .= I + Î± .* Î© .+ Î² .* Î©Â²
    copyto!(t, Jâ‚— * b)
    @inbounds _padpoint!(G, y)
    return y
end

function group_log!(G::SpecialEuclidean, v, y)
    ymat = affine_matrix(G, y)
    logy = real(log(ymat))
    map(copyto!, submanifold_components(G, v), submanifold_components(G, logy))
    _padvector!(G, v)
    return v
end
function group_log!(G::SpecialEuclidean{2}, v, y)
    SO2 = submanifold(G, 2)
    b, Î© = submanifold_components(G, v)
    t, R = submanifold_components(G, y)
    @assert size(b) == (2,)

    group_log!(SO2, Î©, R)
    @inbounds Î¸ = Î©[2]
    Î² = Î¸ / 2
    Î± = Î¸ â‰ˆ 0 ? 1 - Î²^2 / 3 : Î² * cot(Î²)

    @inbounds begin
        b[1] = Î± * t[1] + Î² * t[2]
        b[2] = Î± * t[2] - Î² * t[1]
        _padvector!(G, v)
    end
    return v
end
function group_log!(G::SpecialEuclidean{3}, v, y)
    b, Î© = submanifold_components(G, v)
    t, R = submanifold_components(G, y)
    @assert size(Î©) == (3, 3)
    @assert size(b) == (3,)

    trR = tr(R)
    cosÎ¸ = (trR - 1) / 2
    Î¸ = acos(clamp(cosÎ¸, -1, 1))
    Î¸Â² = Î¸^2
    if Î¸ â‰ˆ 0
        Î± = 1 / 2 + Î¸Â² / 12
        Î² = 1 / 12 + Î¸Â² / 720
    else
        sinÎ¸ = sin(Î¸)
        Î± = Î¸ / sinÎ¸ / 2
        Î² = 1 / Î¸Â² - (1 + cosÎ¸) / 2 / Î¸ / sinÎ¸
    end

    Î© .= (R .- transpose(R)) .* Î±
    Jâ‚—â»Â¹ = I - Î© ./ 2 .+ Î² .* Î©^2
    mul!(b, Jâ‚—â»Â¹, t)
    @inbounds _padvector!(G, v)
    return v
end
