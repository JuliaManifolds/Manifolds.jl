@doc raw"""
    SpecialEuclidean(n)

Special Euclidean group $\mathrm{SE}(n)$, the group of rigid motions.

``\mathrm{SE}(n)`` is the semidirect product of the [`TranslationGroup`](@ref) on $‚Ñù^n$ and
[`SpecialOrthogonal`](@ref)`(n)`

````math
\mathrm{SE}(n) ‚âê \mathrm{T}(n) ‚ãä_Œ∏ \mathrm{SO}(n),
````

where $Œ∏$ is the canonical action of $\mathrm{SO}(n)$ on $\mathrm{T}(n)$ by vector rotation.

This constructor is equivalent to calling

```julia
Tn = TranslationGroup(n)
SOn = SpecialOrthogonal(n)
SemidirectProductGroup(Tn, SOn, RotationAction(Tn, SOn))
```

Points on $\mathrm{SE}(n)$ may be represented as points on the underlying product manifold
$\mathrm{T}(n) √ó \mathrm{SO}(n)$. For group-specific functions, they may also be
represented as affine matrices with size `(n + 1, n + 1)` (see [`affine_matrix`](@ref)), for
which the group operation is [`MultiplicationOperation`](@ref).
"""
const SpecialEuclidean{N} = SemidirectProductGroup{
    ‚Ñù,
    TranslationGroup{Tuple{N},‚Ñù},
    SpecialOrthogonal{N},
    RotationAction{TranslationGroup{Tuple{N},‚Ñù},SpecialOrthogonal{N},LeftAction},
}

const SpecialEuclideanManifold{N} =
    ProductManifold{‚Ñù,Tuple{TranslationGroup{Tuple{N},‚Ñù},SpecialOrthogonal{N}}}

function SpecialEuclidean(n)
    Tn = TranslationGroup(n)
    SOn = SpecialOrthogonal(n)
    A = RotationAction(Tn, SOn)
    return SemidirectProductGroup(Tn, SOn, A)
end

const SpecialEuclideanOperation{N} = SemidirectProductOperation{
    RotationAction{TranslationGroup{Tuple{N},‚Ñù},SpecialOrthogonal{N},LeftAction},
}
const SpecialEuclideanIdentity{N} = Identity{SpecialEuclideanOperation{N}}

Base.show(io::IO, ::SpecialEuclidean{n}) where {n} = print(io, "SpecialEuclidean($(n))")

@inline function active_traits(f, M::SpecialEuclidean, args...)
    return merge_traits(IsGroupManifold(M.op), IsExplicitDecorator())
end

Base.@propagate_inbounds function Base.getindex(
    p::AbstractMatrix,
    M::Union{SpecialEuclidean,SpecialEuclideanManifold},
    i::Union{Integer,Val},
)
    return submanifold_component(M, p, i)
end

Base.@propagate_inbounds function Base.setindex!(
    q::AbstractMatrix,
    p,
    M::Union{SpecialEuclidean,SpecialEuclideanManifold},
    i::Union{Integer,Val},
)
    copyto!(submanifold_component(M, q, i), p)
    return p
end

Base.@propagate_inbounds function submanifold_component(
    ::Union{SpecialEuclidean{n},SpecialEuclideanManifold{n}},
    p::AbstractMatrix,
    ::Val{1},
) where {n}
    return view(p, 1:n, n + 1)
end
Base.@propagate_inbounds function submanifold_component(
    ::Union{SpecialEuclidean{n},SpecialEuclideanManifold{n}},
    p::AbstractMatrix,
    ::Val{2},
) where {n}
    return view(p, 1:n, 1:n)
end

function submanifold_components(
    G::Union{SpecialEuclidean{n},SpecialEuclideanManifold{n}},
    p::AbstractMatrix,
) where {n}
    @assert size(p) == (n + 1, n + 1)
    @inbounds t = submanifold_component(G, p, Val(1))
    @inbounds R = submanifold_component(G, p, Val(2))
    return (t, R)
end

Base.@propagate_inbounds function _padpoint!(
    ::Union{SpecialEuclidean{n},SpecialEuclideanManifold{n}},
    q::AbstractMatrix,
) where {n}
    for i in 1:n
        q[n + 1, i] = 0
    end
    q[n + 1, n + 1] = 1
    return q
end

Base.@propagate_inbounds function _padvector!(
    ::Union{SpecialEuclidean{n},SpecialEuclideanManifold{n}},
    X::AbstractMatrix,
) where {n}
    for i in 1:(n + 1)
        X[n + 1, i] = 0
    end
    return X
end

@doc raw"""
    adjoint_action(::SpecialEuclidean{3}, p, fX::TFVector{<:Any,VeeOrthogonalBasis{‚Ñù}})

Adjoint action of the [`SpecialEuclidean`](@ref) group on the vector with coefficients `fX`
tangent at point `p`.

The formula for the coefficients reads ``t√ó(R‚ãÖœâ) + R‚ãÖr`` for the translation part and
``R‚ãÖœâ`` for the rotation part, where `t` is the translation part of `p`, `R` is the rotation
matrix part of `p`, `r` is the translation part of `fX` and `œâ` is the rotation part of `fX`,
``√ó`` is the cross product and ``‚ãÖ`` is the matrix product.
"""
function adjoint_action(::SpecialEuclidean{3}, p, fX::TFVector{<:Any,VeeOrthogonalBasis{‚Ñù}})
    t = p.parts[1]
    R = p.parts[2]
    r = fX.data[SA[1, 2, 3]]
    œâ = fX.data[SA[4, 5, 6]]
    Rœâ = R * œâ
    return TFVector([cross(t, Rœâ) + R * r; Rœâ], fX.basis)
end

@doc raw"""
    affine_matrix(G::SpecialEuclidean, p) -> AbstractMatrix

Represent the point $p ‚àà \mathrm{SE}(n)$ as an affine matrix.
For $p = (t, R) ‚àà \mathrm{SE}(n)$, where $t ‚àà \mathrm{T}(n), R ‚àà \mathrm{SO}(n)$, the
affine representation is the $n + 1 √ó n + 1$ matrix

````math
\begin{pmatrix}
R & t \\
0^\mathrm{T} & 1
\end{pmatrix}.
````

This function embeds $\mathrm{SE}(n)$ in the general linear group $\mathrm{GL}(n+1)$.
It is an isometric embedding and group homomorphism [^RicoMartinez1988].

See also [`screw_matrix`](@ref) for matrix representations of the Lie algebra.

[^RicoMartinez1988]:
    > Rico Martinez, J. M., ‚ÄúRepresentations of the Euclidean group and its applications
    > to the kinematics of spatial chains,‚Äù PhD Thesis, University of Florida, 1988.
"""
function affine_matrix(G::SpecialEuclidean{n}, p) where {n}
    pis = submanifold_components(G, p)
    pmat = allocate_result(G, affine_matrix, pis...)
    map(copyto!, submanifold_components(G, pmat), pis)
    @inbounds _padpoint!(G, pmat)
    return pmat
end
affine_matrix(::SpecialEuclidean{n}, p::AbstractMatrix) where {n} = p
function affine_matrix(::SpecialEuclidean{n}, ::SpecialEuclideanIdentity{n}) where {n}
    s = maybesize(Size(n, n))
    s isa Size && return SDiagonal{n,Float64}(I)
    return Diagonal{Float64}(I, n)
end

function check_point(G::SpecialEuclideanManifold{n}, p::AbstractMatrix; kwargs...) where {n}
    errs = DomainError[]
    # homogeneous
    if !isapprox(p[end, :], [zeros(size(p, 2) - 1)..., 1]; kwargs...)
        push!(
            errs,
            DomainError(
                p[end, :],
                "The last row of $p is not homogeneous, i.e. of form [0,..,0,1].",
            ),
        )
    end
    # translate part
    err2 = check_point(submanifold(G, 1), p[1:n, end]; kwargs...)
    !isnothing(err2) && push!(errs, err2)
    # SOn
    err3 = check_point(submanifold(G, 2), p[1:n, 1:n]; kwargs...)
    !isnothing(err3) && push!(errs, err3)
    if length(errs) > 1
        return CompositeManifoldError(errs)
    end
    return length(errs) == 0 ? nothing : first(errs)
end

function check_size(G::SpecialEuclideanManifold{n}, p::AbstractMatrix; kwargs...) where {n}
    return check_size(Euclidean(n + 1, n + 1), p)
end
function check_size(
    G::SpecialEuclideanManifold{n},
    p::AbstractMatrix,
    X::AbstractMatrix;
    kwargs...,
) where {n}
    return check_size(Euclidean(n + 1, n + 1), X)
end

function check_vector(
    G::SpecialEuclideanManifold{n},
    p::AbstractMatrix,
    X::AbstractMatrix;
    kwargs...,
) where {n}
    errs = DomainError[]
    # homogeneous
    if !isapprox(X[end, :], zeros(size(X, 2)); kwargs...)
        push!(
            errs,
            DomainError(
                X[end, :],
                "The last row of $X is not homogeneous, i.e. of form [0,..,0,0].",
            ),
        )
    end
    # translate part
    err2 = check_vector(submanifold(G, 1), p[1:n, end], X[1:n, end]; kwargs...)
    !isnothing(err2) && push!(errs, err2)
    # SOn
    err3 = check_vector(submanifold(G, 2), p[1:n, 1:n], X[1:n, 1:n]; kwargs...)
    !isnothing(err3) && push!(errs, err3)
    if length(errs) > 1
        return CompositeManifoldError(errs)
    end
    return length(errs) == 0 ? nothing : first(errs)
end

@doc raw"""
    screw_matrix(G::SpecialEuclidean, X) -> AbstractMatrix

Represent the Lie algebra element $X ‚àà ùî∞ùî¢(n) = T_e \mathrm{SE}(n)$ as a screw matrix.
For $X = (b, Œ©) ‚àà ùî∞ùî¢(n)$, where $Œ© ‚àà ùî∞ùî¨(n) = T_e \mathrm{SO}(n)$, the screw representation is
the $n + 1 √ó n + 1$ matrix

````math
\begin{pmatrix}
Œ© & b \\
0^\mathrm{T} & 0
\end{pmatrix}.
````

This function embeds $ùî∞ùî¢(n)$ in the general linear Lie algebra $ùî§ùî©(n+1)$ but it's not
a homomorphic embedding (see [`SpecialEuclideanInGeneralLinear`](@ref) for a homomorphic one).

See also [`affine_matrix`](@ref) for matrix representations of the Lie group.
"""
function screw_matrix(G::SpecialEuclidean{n}, X) where {n}
    Xis = submanifold_components(G, X)
    Xmat = allocate_result(G, screw_matrix, Xis...)
    map(copyto!, submanifold_components(G, Xmat), Xis)
    @inbounds _padvector!(G, Xmat)
    return Xmat
end
screw_matrix(::SpecialEuclidean{n}, X::AbstractMatrix) where {n} = X

function allocate_result(::SpecialEuclidean{n}, ::typeof(affine_matrix), p...) where {n}
    return allocate(p[1], Size(n + 1, n + 1))
end
function allocate_result(::SpecialEuclidean{n}, ::typeof(screw_matrix), X...) where {n}
    return allocate(X[1], Size(n + 1, n + 1))
end

compose(::SpecialEuclidean, p::AbstractMatrix, q::AbstractMatrix) = p * q

function compose!(
    ::SpecialEuclidean,
    x::AbstractMatrix,
    p::AbstractMatrix,
    q::AbstractMatrix,
)
    copyto!(x, p * q)
    return x
end

# More generic default was mostly OK but it lacks padding
function exp!(M::SpecialEuclideanManifold, q::AbstractMatrix, p, X)
    map(
        exp!,
        M.manifolds,
        submanifold_components(M, q),
        submanifold_components(M, p),
        submanifold_components(M, X),
    )
    @inbounds _padpoint!(M, q)
    return q
end
function exp!(M::SpecialEuclideanManifold, q::AbstractMatrix, p, X, t::Number)
    map(
        (N, qc, pc, Xc) -> exp!(N, qc, pc, Xc, t),
        M.manifolds,
        submanifold_components(M, q),
        submanifold_components(M, p),
        submanifold_components(M, X),
    )
    @inbounds _padpoint!(M, q)
    return q
end

@doc raw"""
    exp_lie(G::SpecialEuclidean{n}, X)

Compute the group exponential of $X = (b, Œ©) ‚àà ùî∞ùî¢(n)$, where $b ‚àà ùî±(n)$ and $Œ© ‚àà ùî∞ùî¨(n)$:

````math
\exp X = (t, R),
````

where $t ‚àà \mathrm{T}(n)$ and $R = \exp Œ©$ is the group exponential on $\mathrm{SO}(n)$.

In the [`screw_matrix`](@ref) representation, the group exponential is the matrix
exponential (see [`exp_lie`](@ref)).
"""
exp_lie(::SpecialEuclidean, ::Any)

@doc raw"""
    exp_lie(G::SpecialEuclidean{2}, X)

Compute the group exponential of $X = (b, Œ©) ‚àà ùî∞ùî¢(2)$, where $b ‚àà ùî±(2)$ and $Œ© ‚àà ùî∞ùî¨(2)$:

````math
\exp X = (t, R) = (U(Œ∏) b, \exp Œ©),
````

where $t ‚àà \mathrm{T}(2)$, $R = \exp Œ©$ is the group exponential on $\mathrm{SO}(2)$,

````math
U(Œ∏) = \frac{\sin Œ∏}{Œ∏} I_2 + \frac{1 - \cos Œ∏}{Œ∏^2} Œ©,
````

and $Œ∏ = \frac{1}{\sqrt{2}} \lVert Œ© \rVert_e$
(see [`norm`](@ref norm(M::Rotations, p, X))) is the angle of the rotation.
"""
exp_lie(::SpecialEuclidean{2}, ::Any)

@doc raw"""
    exp_lie(G::SpecialEuclidean{3}, X)

Compute the group exponential of $X = (b, Œ©) ‚àà ùî∞ùî¢(3)$, where $b ‚àà ùî±(3)$ and $Œ© ‚àà ùî∞ùî¨(3)$:

````math
\exp X = (t, R) = (U(Œ∏) b, \exp Œ©),
````

where $t ‚àà \mathrm{T}(3)$, $R = \exp Œ©$ is the group exponential on $\mathrm{SO}(3)$,

````math
U(Œ∏) = I_3 + \frac{1 - \cos Œ∏}{Œ∏^2} Œ© + \frac{Œ∏ - \sin Œ∏}{Œ∏^3} Œ©^2,
````

and $Œ∏ = \frac{1}{\sqrt{2}} \lVert Œ© \rVert_e$
(see [`norm`](@ref norm(M::Rotations, p, X))) is the angle of the rotation.
"""
exp_lie(::SpecialEuclidean{3}, ::Any)

function exp_lie!(G::SpecialEuclidean, q, X)
    Xmat = screw_matrix(G, X)
    qmat = exp(Xmat)
    map(copyto!, submanifold_components(G, q), submanifold_components(G, qmat))
    _padpoint!(G, q)
    return q
end
function exp_lie!(G::SpecialEuclidean{2}, q, X)
    SO2 = submanifold(G, 2)
    b, Œ© = submanifold_components(G, X)
    t, R = submanifold_components(G, q)
    @assert size(R) == (2, 2)
    @assert size(t) == (2,)
    @assert size(b) == (2,)

    Œ∏ = vee(SO2, identity_element(SO2, R), Œ©)[1]
    sinŒ∏, cosŒ∏ = sincos(Œ∏)
    if Œ∏ ‚âà 0
        Œ± = 1 - Œ∏^2 / 6
        Œ≤ = Œ∏ / 2
    else
        Œ± = sinŒ∏ / Œ∏
        Œ≤ = (1 - cosŒ∏) / Œ∏
    end

    @inbounds begin
        R[1] = cosŒ∏
        R[2] = sinŒ∏
        R[3] = -sinŒ∏
        R[4] = cosŒ∏
        t[1] = Œ± * b[1] - Œ≤ * b[2]
        t[2] = Œ± * b[2] + Œ≤ * b[1]
        _padpoint!(G, q)
    end
    return q
end
function exp_lie!(G::SpecialEuclidean{3}, q, X)
    SO3 = submanifold(G, 2)
    b, Œ© = submanifold_components(G, X)
    t, R = submanifold_components(G, q)
    @assert size(R) == (3, 3)
    @assert size(t) == (3,)

    Œ∏ = norm(SO3, Identity(SO3), Œ©) / sqrt(2)
    Œ∏¬≤ = Œ∏^2
    if Œ∏ ‚âà 0
        Œ± = 1 - Œ∏¬≤ / 6
        Œ≤ = Œ∏ / 2
        Œ≥ = 1 / 6 - Œ∏¬≤ / 120
    else
        sinŒ∏, cosŒ∏ = sincos(Œ∏)
        Œ± = sinŒ∏ / Œ∏
        Œ≤ = (1 - cosŒ∏) / Œ∏¬≤
        Œ≥ = (1 - Œ±) / Œ∏¬≤
    end

    Œ©¬≤ = Œ©^2
    J‚Çó = I + Œ≤ .* Œ© .+ Œ≥ .* Œ©¬≤
    R .= I + Œ± .* Œ© .+ Œ≤ .* Œ©¬≤
    copyto!(t, J‚Çó * b)
    @inbounds _padpoint!(G, q)
    return q
end

function isapprox(M::SpecialEuclidean, p, q; kwargs...)
    tp, Rp = submanifold_components(M, p)
    tq, Rq = submanifold_components(M, q)
    return isapprox(tp, tq; kwargs...) && isapprox(Rp, Rq; kwargs...)
end
function isapprox(
    M::SpecialEuclidean{N},
    p::SpecialEuclideanIdentity{N},
    q;
    kwargs...,
) where {N}
    return is_identity(M, q)
end
function isapprox(
    M::SpecialEuclidean{N},
    p,
    q::SpecialEuclideanIdentity{N};
    kwargs...,
) where {N}
    return is_identity(M, p)
end
function isapprox(
    M::SpecialEuclidean{N},
    p::SpecialEuclideanIdentity{N},
    q::SpecialEuclideanIdentity{N};
    kwargs...,
) where {N}
    return true
end
function isapprox(M::SpecialEuclidean, p, X, Y; kwargs...)
    tX, RX = submanifold_components(M, X)
    tY, RY = submanifold_components(M, Y)
    return isapprox(tX, tY; kwargs...) && isapprox(RX, RY; kwargs...)
end

@doc raw"""
    log_lie(G::SpecialEuclidean{n}, p) where {n}

Compute the group logarithm of $p = (t, R) ‚àà \mathrm{SE}(n)$, where $t ‚àà \mathrm{T}(n)$
and $R ‚àà \mathrm{SO}(n)$:

````math
\log p = (b, Œ©),
````

where $b ‚àà ùî±(n)$ and $Œ© = \log R ‚àà ùî∞ùî¨(n)$ is the group logarithm on $\mathrm{SO}(n)$.

In the [`affine_matrix`](@ref) representation, the group logarithm is the matrix logarithm
(see [`log_lie`](@ref)):
"""
log_lie(::SpecialEuclidean, ::Any)

@doc raw"""
    log_lie(G::SpecialEuclidean{2}, p)

Compute the group logarithm of $p = (t, R) ‚àà \mathrm{SE}(2)$, where $t ‚àà \mathrm{T}(2)$
and $R ‚àà \mathrm{SO}(2)$:

````math
\log p = (b, Œ©) = (U(Œ∏)^{-1} t, \log R),
````

where $b ‚àà ùî±(2)$, $Œ© = \log R ‚àà ùî∞ùî¨(2)$ is the group logarithm on $\mathrm{SO}(2)$,

````math
U(Œ∏) = \frac{\sin Œ∏}{Œ∏} I_2 + \frac{1 - \cos Œ∏}{Œ∏^2} Œ©,
````

and $Œ∏ = \frac{1}{\sqrt{2}} \lVert Œ© \rVert_e$
(see [`norm`](@ref norm(M::Rotations, p, X))) is the angle of the rotation.
"""
log_lie(::SpecialEuclidean{2}, ::Any)

@doc raw"""
    log_lie(G::SpecialEuclidean{3}, p)

Compute the group logarithm of $p = (t, R) ‚àà \mathrm{SE}(3)$, where $t ‚àà \mathrm{T}(3)$
and $R ‚àà \mathrm{SO}(3)$:

````math
\log p = (b, Œ©) = (U(Œ∏)^{-1} t, \log R),
````

where $b ‚àà ùî±(3)$, $Œ© = \log R ‚àà ùî∞ùî¨(3)$ is the group logarithm on $\mathrm{SO}(3)$,

````math
U(Œ∏) = I_3 + \frac{1 - \cos Œ∏}{Œ∏^2} Œ© + \frac{Œ∏ - \sin Œ∏}{Œ∏^3} Œ©^2,
````

and $Œ∏ = \frac{1}{\sqrt{2}} \lVert Œ© \rVert_e$
(see [`norm`](@ref norm(M::Rotations, p, X))) is the angle of the rotation.
"""
log_lie(::SpecialEuclidean{3}, ::Any)

function _log_lie!(G::SpecialEuclidean, X, q)
    qmat = affine_matrix(G, q)
    Xmat = real(log_safe(qmat))
    map(copyto!, submanifold_components(G, X), submanifold_components(G, Xmat))
    _padvector!(G, X)
    return X
end
function _log_lie!(G::SpecialEuclidean{2}, X, q)
    SO2 = submanifold(G, 2)
    b, Œ© = submanifold_components(G, X)
    t, R = submanifold_components(G, q)
    @assert size(b) == (2,)

    log_lie!(SO2, Œ©, R)
    @inbounds Œ∏ = Œ©[2]
    Œ≤ = Œ∏ / 2
    Œ± = Œ∏ ‚âà 0 ? 1 - Œ≤^2 / 3 : Œ≤ * cot(Œ≤)

    @inbounds begin
        b[1] = Œ± * t[1] + Œ≤ * t[2]
        b[2] = Œ± * t[2] - Œ≤ * t[1]
        _padvector!(G, X)
    end
    return X
end
function _log_lie!(G::SpecialEuclidean{3}, X, q)
    b, Œ© = submanifold_components(G, X)
    t, R = submanifold_components(G, q)
    @assert size(Œ©) == (3, 3)
    @assert size(b) == (3,)

    trR = tr(R)
    cosŒ∏ = (trR - 1) / 2
    Œ∏ = acos(clamp(cosŒ∏, -1, 1))
    Œ∏¬≤ = Œ∏^2
    if Œ∏ ‚âà 0
        Œ± = 1 / 2 + Œ∏¬≤ / 12
        Œ≤ = 1 / 12 + Œ∏¬≤ / 720
    else
        sinŒ∏ = sin(Œ∏)
        Œ± = Œ∏ / sinŒ∏ / 2
        Œ≤ = 1 / Œ∏¬≤ - (1 + cosŒ∏) / 2 / Œ∏ / sinŒ∏
    end

    Œ© .= (R .- transpose(R)) .* Œ±
    J‚Çó‚Åª¬π = I - Œ© ./ 2 .+ Œ≤ .* Œ©^2
    mul!(b, J‚Çó‚Åª¬π, t)
    @inbounds _padvector!(G, X)
    return X
end

# More generic default was mostly OK but it lacks padding
function log!(M::SpecialEuclideanManifold, X::AbstractMatrix, p, q)
    map(
        log!,
        M.manifolds,
        submanifold_components(M, X),
        submanifold_components(M, p),
        submanifold_components(M, q),
    )
    @inbounds _padvector!(M, X)
    return X
end

"""
    lie_bracket(G::SpecialEuclidean, X::ProductRepr, Y::ProductRepr)
    lie_bracket(G::SpecialEuclidean, X::AbstractMatrix, Y::AbstractMatrix)

Calculate the Lie bracket between elements `X` and `Y` of the special Euclidean Lie
algebra. For the matrix representation (which can be obtained using [`screw_matrix`](@ref))
the formula is ``[X, Y] = XY-YX``, while in the [`ProductRepr`](@ref) representation the
formula reads ``[X, Y] = [(t_1, R_1), (t_2, R_2)] = (R_1 t_2 - R_2 t_1, R_1 R_2 - R_2 R_1)``.
"""
function lie_bracket(G::SpecialEuclidean, X::ProductRepr, Y::ProductRepr)
    nX, hX = submanifold_components(G, X)
    nY, hY = submanifold_components(G, Y)
    return ProductRepr(hX * nY - hY * nX, lie_bracket(G.manifold.manifolds[2], hX, hY))
end
function lie_bracket(::SpecialEuclidean, X::AbstractMatrix, Y::AbstractMatrix)
    return X * Y - Y * X
end

function lie_bracket!(G::SpecialEuclidean, Z, X, Y)
    nX, hX = submanifold_components(G, X)
    nY, hY = submanifold_components(G, Y)
    nZ, hZ = submanifold_components(G, Z)
    lie_bracket!(G.manifold.manifolds[2], hZ, hX, hY)
    nZ .= hX * nY .- hY * nX
    @inbounds _padvector!(G, Z)
    return Z
end

"""
    translate_diff(G::SpecialEuclidean, p, q, X, ::RightAction)

Differential of the right action of the [`SpecialEuclidean`](@ref) group on itself.
The formula for the rotation part is the differential of the right rotation action, while
the formula for the translation part reads
````math
R_q‚ãÖX_R‚ãÖt_p + X_t
````
where ``R_q`` is the rotation part of `q`, ``X_R`` is the rotation part of `X`, ``t_p``
is the translation part of `p` and ``X_t`` is the translation part of `X`.
"""
translate_diff(G::SpecialEuclidean, p, q, X, ::RightAction)

function translate_diff!(G::SpecialEuclidean, Y, p, q, X, ::RightAction)
    np, hp = submanifold_components(G, p)
    nq, hq = submanifold_components(G, q)
    nX, hX = submanifold_components(G, X)
    nY, hY = submanifold_components(G, Y)
    hY .= hp' * hX * hp
    copyto!(nY, hq * (hX * np) + nX)
    @inbounds _padvector!(G, Y)
    return Y
end

@doc raw"""
    SpecialEuclideanInGeneralLinear

An explicit isometric and homomorphic embedding of $\mathrm{SE}(n)$ in $\mathrm{GL}(n+1)$
and $ùî∞ùî¢(n)$ in $ùî§ùî©(n+1)$.
Note that this is *not* a transparently isometric embedding.

# Constructor

    SpecialEuclideanInGeneralLinear(n)
"""
const SpecialEuclideanInGeneralLinear =
    EmbeddedManifold{‚Ñù,<:SpecialEuclidean,<:GeneralLinear}

function SpecialEuclideanInGeneralLinear(n)
    return EmbeddedManifold(SpecialEuclidean(n), GeneralLinear(n + 1))
end

"""
    embed(M::SpecialEuclideanInGeneralLinear, p)

Embed the point `p` on [`SpecialEuclidean`](@ref) in the [`GeneralLinear`](@ref) group.
The embedding is calculated using [`affine_matrix`](@ref).
"""
function embed(M::SpecialEuclideanInGeneralLinear, p)
    G = M.manifold
    return affine_matrix(G, p)
end
"""
    embed(M::SpecialEuclideanInGeneralLinear, p, X)

Embed the tangent vector X at point `p` on [`SpecialEuclidean`](@ref) in the
[`GeneralLinear`](@ref) group. Point `p` can use any representation valid for
`SpecialEuclidean`. The embedding is similar from the one defined by [`screw_matrix`](@ref)
but the translation part is multiplied by inverse of the rotation part.
"""
function embed(M::SpecialEuclideanInGeneralLinear, p, X)
    G = M.manifold
    np, hp = submanifold_components(G, p)
    nX, hX = submanifold_components(G, X)
    Y = allocate_result(G, screw_matrix, nX, hX)
    nY, hY = submanifold_components(G, Y)
    copyto!(hY, hX)
    copyto!(nY, hp' * nX)
    @inbounds _padvector!(G, Y)
    return Y
end

function embed!(M::SpecialEuclideanInGeneralLinear, q, p)
    return copyto!(q, embed(M, p))
end
function embed!(M::SpecialEuclideanInGeneralLinear, Y, p, X)
    return copyto!(Y, embed(M, p, X))
end

"""
    project(M::SpecialEuclideanInGeneralLinear, p)

Project point `p` in [`GeneralLinear`](@ref) to the [`SpecialEuclidean`](@ref) group.
This is performed by extracting the rotation and translation part as in [`affine_matrix`](@ref).
"""
function project(M::SpecialEuclideanInGeneralLinear, p)
    G = M.manifold
    np, hp = submanifold_components(G, p)
    return ProductRepr(np, hp)
end
"""
    project(M::SpecialEuclideanInGeneralLinear, p, X)

Project tangent vector `X` at point `p` in [`GeneralLinear`](@ref) to the
[`SpecialEuclidean`](@ref) Lie algebra.
This reverses the transformation performed by [`embed`](@ref embed(M::SpecialEuclideanInGeneralLinear, p, X))
"""
function project(M::SpecialEuclideanInGeneralLinear, p, X)
    G = M.manifold
    np, hp = submanifold_components(G, p)
    nX, hX = submanifold_components(G, X)
    return ProductRepr(hp * nX, hX)
end

function project!(M::SpecialEuclideanInGeneralLinear, q, p)
    return copyto!(q, project(M, p))
end
function project!(M::SpecialEuclideanInGeneralLinear, Y, p, X)
    return copyto!(Y, project(M, p, X))
end
