
"""
    AbstractCartanSchoutenConnection

Abstract type for Cartan-Schouten connections, that is connections whose geodesics
going through group identity are one-parameter subgroups. See[^Pennec2020] for details.

[^Pennec2020]:
    > X. Pennec and M. Lorenzi, â€œ5 - Beyond Riemannian geometry: The affine connection
    > setting for transformation groups,â€ in Riemannian Geometric Statistics in Medical Image
    > Analysis, X. Pennec, S. Sommer, and T. Fletcher, Eds. Academic Press, 2020, pp. 169â€“229.
    > doi: 10.1016/B978-0-12-814725-2.00012-1.
"""
abstract type AbstractCartanSchoutenConnection <: AbstractAffineConnection end

"""
    CartanSchoutenMinus

The unique Cartan-Schouten connection such that all left-invariant vector fields are
globally defined by their value at identity. It is biinvariant with respect to the group
operation.
"""
struct CartanSchoutenMinus <: AbstractCartanSchoutenConnection end

"""
    CartanSchoutenPlus

The unique Cartan-Schouten connection such that all right-invariant vector fields are
globally defined by their value at identity. It is biinvariant with respect to the group
operation.
"""
struct CartanSchoutenPlus <: AbstractCartanSchoutenConnection end

"""
    CartanSchoutenZero

The unique torsion-free Cartan-Schouten connection. It is biinvariant with respect to the
group operation.

If the metric on the underlying manifold is bi-invariant then it is equivalent to the
Levi-Civita connection of that metric.
"""
struct CartanSchoutenZero <: AbstractCartanSchoutenConnection end

const CartanSchoutenMinusGroup{ð”½,M} = ConnectionManifold{ð”½,M,CartanSchoutenMinus}
const CartanSchoutenPlusGroup{ð”½,M} = ConnectionManifold{ð”½,M,CartanSchoutenPlus}
const CartanSchoutenZeroGroup{ð”½,M} = ConnectionManifold{ð”½,M,CartanSchoutenZero}

"""
    exp(M::ConnectionManifold{ð”½,<:AbstractDecoratorManifold{ð”½},<:AbstractCartanSchoutenConnection}, p, X) where {ð”½}

Compute the exponential map on the [`ConnectionManifold`](@ref) `M` with a Cartan-Schouten
connection. See Sections 5.3.2 and 5.3.3 of [^Pennec2020] for details.
"""
function exp(
    M::ConnectionManifold{
        ð”½,
        <:AbstractDecoratorManifold{ð”½},
        <:AbstractCartanSchoutenConnection,
    },
    p,
    X,
) where {ð”½}
    Y = inverse_translate_diff(M.manifold, p, p, X, LeftAction())
    return compose(M.manifold, p, exp_lie(M.manifold, Y))
end

function exp!(
    M::ConnectionManifold{
        ð”½,
        <:AbstractDecoratorManifold{ð”½},
        <:AbstractCartanSchoutenConnection,
    },
    q,
    p,
    X,
) where {ð”½}
    Y = inverse_translate_diff(M.manifold, p, p, X, LeftAction())
    return compose!(M.manifold, q, p, exp_lie(M.manifold, Y))
end

"""
    log(M::ConnectionManifold{ð”½,<:AbstractDecoratorManifold{ð”½},<:AbstractCartanSchoutenConnection}, p, q) where {ð”½}

Compute the logarithmic map on the [`ConnectionManifold`](@ref) `M` with a Cartan-Schouten
connection. See Sections 5.3.2 and 5.3.3 of [^Pennec2020] for details.
"""
function log(
    M::ConnectionManifold{
        ð”½,
        <:AbstractDecoratorManifold{ð”½},
        <:AbstractCartanSchoutenConnection,
    },
    Y,
    p,
    q,
) where {ð”½}
    pinvq = compose(M.manifold, inv(M.manifold, p), q)
    Y = log_lie(M.manifold, pinvq)
    return translate_diff(M.manifold, p, Identity(M.manifold), Y, LeftAction())
end

function log!(
    M::ConnectionManifold{
        ð”½,
        <:AbstractDecoratorManifold{ð”½},
        <:AbstractCartanSchoutenConnection,
    },
    Y,
    p,
    q,
) where {ð”½}
    pinvq = compose(M.manifold, inv(M.manifold, p), q)
    log_lie!(M.manifold, Y, pinvq)
    return translate_diff!(M.manifold, Y, p, Identity(M.manifold), Y)
end

"""
    parallel_transport_to(M::CartanSchoutenMinusGroup, p, X, q)

Transport tangent vector `X` at point `p` on the group manifold `M` with the
[`CartanSchoutenMinus`](@ref) connection to point `q`. See [^Pennec2020] for details.
"""
function parallel_transport_to(M::CartanSchoutenMinusGroup, p, X, q)
    return inverse_translate_diff(M.manifold, q, p, X, LeftAction())
end

function parallel_transport_to!(M::CartanSchoutenMinusGroup, Y, p, X, q)
    return inverse_translate_diff!(M.manifold, Y, q, p, X, LeftAction())
end

"""
    vector_transport_to(M::CartanSchoutenPlusGroup, p, X, q)

Transport tangent vector `X` at point `p` on the group manifold `M` with the
[`CartanSchoutenPlus`](@ref) connection to point `q`. See [^Pennec2020] for details.
"""
parallel_transport_to(M::CartanSchoutenPlusGroup, p, X, q)

function parallel_transport_to!(M::CartanSchoutenPlusGroup, Y, p, X, q)
    return inverse_translate_diff!(M.manifold, Y, q, p, X, RightAction())
end

"""
    parallel_transport_direction(M::CartanSchoutenZeroGroup, ::Identity, X, d)

Transport tangent vector `X` at identity on the group manifold with the
[`CartanSchoutenZero`](@ref) connection in the direction `d`. See [^Pennec2020] for details.
"""
function parallel_transport_direction(M::CartanSchoutenZeroGroup, p::Identity, X, d)
    dexp_half = exp_lie(M.manifold, d / 2)
    Y = translate_diff(M.manifold, dexp_half, p, X, RightAction())
    return translate_diff(M.manifold, dexp_half, p, Y, LeftAction())
end

function parallel_transport_direction!(M::CartanSchoutenZeroGroup, Y, p::Identity, X, d)
    dexp_half = exp_lie(M.manifold, d / 2)
    translate_diff!(M.manifold, Y, dexp_half, p, X, RightAction())
    return translate_diff!(M.manifold, Y, dexp_half, p, Y, LeftAction())
end

"""
    parallel_transport_to(M::CartanSchoutenZeroGroup, p::Identity, X, q)

Transport vector `X` at identity of group `M` equipped with the [`CartanSchoutenZero`](@ref)
connection to point `q` using parallel transport.
"""
function parallel_transport_to(M::CartanSchoutenZeroGroup, p::Identity, X, q)
    d = log_lie(M.manifold, q)
    return parallel_transport_direction(M, p, X, d)
end

function parallel_transport_to!(M::CartanSchoutenZeroGroup, Y, p::Identity, X, q)
    d = log_lie(M.manifold, q)
    return parallel_transport_direction!(M, Y, p, X, d)
end
