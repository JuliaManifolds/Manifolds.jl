@doc raw"""
    GeneralLinear{n,ğ”½} <:
        AbstractGroupManifold{ğ”½,MultiplicationOperation,DefaultEmbeddingType}

The general linear group, that is, the group of all invertible matrices in $ğ”½^{nÃ—n}$.

The default metric is the left-$\mathrm{GL}(n)$-right-$\mathrm{O}(n)$-invariant metric whose
inner product is written
$$âŸ¨X_p,Y_pâŸ©_p = âŸ¨p^{-1}X_p,p^{-1}Y_pâŸ©_\mathrm{F} = âŸ¨X_e, Y_eâŸ©_\mathrm{F},$$
where $X_e = p^{-1}X_p âˆˆ ğ”¤l(n) = T_e \mathrm{GL}(n, ğ”½) = ğ”½^{nÃ—n}$ is the corresponding
vector in the Lie algebra. In the default implementations, all tangent vectors $X_p$ are
instead represented with their corresponding Lie algebra vectors.

[^MartinNeff2016]:
    > Martin, R. J. and Neff, P.:
    > â€œMinimal geodesics on GL(n) for left-invariant, right-O(n)-invariant Riemannian metricsâ€,
    > Journal of Geometric Mechanics 8(3), pp. 323-357, 2016.
    > doi: [10.3934/jgm.2016010](https://doi.org/10.3934/jgm.2016010),
    > arXiv: [1409.7849v2](https://arxiv.org/abs/1409.7849v2).
[^AndruchowLarotondaRechtVarela2014]:
    > Andruchow E., Larotonda G., Recht L., and Varela A.:
    > â€œThe left invariant metric in the general linear groupâ€,
    > Journal of Geometry and Physics 86, pp. 241-257, 2014.
    > doi: [10.1016/j.geomphys.2014.08.009](https://doi.org/10.1016/j.geomphys.2014.08.009),
    > arXiv: [1109.0520v1](https://arxiv.org/abs/1109.0520v1).
"""
struct GeneralLinear{n,ğ”½} <:
       AbstractGroupManifold{ğ”½,MultiplicationOperation,DefaultEmbeddingType} end

GeneralLinear(n, ğ”½::AbstractNumbers = â„) = GeneralLinear{n,ğ”½}()

function allocation_promotion_function(::GeneralLinear{n,â„‚}, f, ::Tuple) where {n}
    return complex
end

function check_manifold_point(G::GeneralLinear{n,ğ”½}, p; kwargs...) where {n,ğ”½}
    mpv = check_manifold_point(decorated_manifold(G), p; kwargs...)
    mpv === nothing || return mpv
    detp = det(p)
    if iszero(detp)
        return DomainError(
            detp,
            "The matrix $(p) does not lie on $(G), since it is not invertible.",
        )
    end
    return nothing
end

function check_tangent_vector(
    G::GeneralLinear{n,ğ”½},
    p,
    X;
    check_base_point = true,
    kwargs...,
) where {n,ğ”½}
    if check_base_point
        mpe = check_manifold_point(G, p; kwargs...)
        mpe === nothing || return mpe
    end
    mpv = check_tangent_vector(decorated_manifold(G), p, X; kwargs...)
    mpv === nothing || return mpv
    return nothing
end

decorated_manifold(::GeneralLinear{n,ğ”½}) where {n,ğ”½} = Euclidean(n, n; field = ğ”½)

default_metric_dispatch(::GeneralLinear, ::EuclideanMetric) = Val(true)
default_metric_dispatch(::GeneralLinear, ::LeftInvariantMetric{EuclideanMetric}) = Val(true)

function exp!(G::GeneralLinear, q, p, X)
    expX = exp(X)
    if isnormal(X; atol = sqrt(eps(real(eltype(X)))))
        return compose!(G, q, p, expX)
    end
    compose!(G, q, expX', exp(X - X'))
    compose!(G, q, p, q)
    return q
end
function exp!(::GeneralLinear{1}, q, p, X)
    p1 = p isa Identity ? p : p[1]
    q[1] = p1 * exp(X[1])
    return q
end
function exp!(G::GeneralLinear{2}, q, p, X)
    if isnormal(X; atol = sqrt(eps(real(eltype(X)))))
        return compose!(G, q, p, exp(SizedMatrix{2,2}(X)))
    end
    A = SizedMatrix{2,2}(X')
    B = SizedMatrix{2,2}(X) - A
    compose!(G, q, exp(A), exp(B))
    compose!(G, q, p, q)
    return q
end

flat!(::GeneralLinear, Î¾::CoTFVector, p, X::TFVector) = copyto!(Î¾, X)

get_coordinates(::GeneralLinear{n,â„}, p, X, ::DefaultOrthonormalBasis) where {n} = vec(X)

function get_coordinates!(
    ::GeneralLinear{n,â„},
    Xâ±,
    p,
    X,
    ::DefaultOrthonormalBasis,
) where {n}
    return copyto!(Xâ±, X)
end

function get_vector(::GeneralLinear{n,â„}, p, Xâ±, ::DefaultOrthonormalBasis) where {n}
    return reshape(Xâ±, n, n)
end

function get_vector!(::GeneralLinear{n,â„}, X, p, Xâ±, ::DefaultOrthonormalBasis) where {n}
    return copyto!(X, Xâ±)
end

function group_exp!(::GeneralLinear{1}, q, X)
    q[1] = exp(X[1])
    return q
end
group_exp!(::GeneralLinear{2}, q, X) = copyto!(q, exp(SizedMatrix{2,2}(X)))

function group_log!(::GeneralLinear{1}, X, p)
    X[1] = log(p[1])
    return X
end

inner(::GeneralLinear, p, X, Y) = dot(X, Y)

invariant_metric_dispatch(::GeneralLinear, ::LeftAction) = Val(true)

inverse_translate(::GeneralLinear, p, q, ::LeftAction) = p \ q
inverse_translate(::GeneralLinear, p, q, ::RightAction) = q / p

inverse_translate_diff(::GeneralLinear, p, q, X, ::LeftAction) = X
inverse_translate_diff(::GeneralLinear, p, q, X, ::RightAction) = p * X / p

function inverse_translate_diff!(G::GeneralLinear, Y, p, q, X, conv::ActionDirection)
    return copyto!(Y, inverse_translate_diff(G, p, q, X, conv))
end

function _log_project_SOn_Sâº!(X, q, n = size(q, 1))
    F = svd(q)
    d = allocate(q, n)
    s = mean(F.S)
    fill!(d, s)
    d[n] *= det(q) / prod(F.S) # adjust sign of determinant
    expX = F.U * Diagonal(d) * F.Vt
    return log_safe!(X, expX)
end

function log!(G::GeneralLinear{n}, X, p, q) where {n}
    pinvq = inverse_translate(G, p, q, LeftAction())
    number_system(G) === â„ && det(pinvq) â‰¤ 0 && throw(OutOfInjectivityRadiusError())
    e = Identity(G, pinvq)
    if isnormal(pinvq; atol = sqrt(eps(real(eltype(pinvq)))))
        log_safe!(X, pinvq)
    else
        ğ”½ = number_system(G)
        if ğ”½ === â„
            _log_project_SOn_Sâº!(X, pinvq, n)
            inverse_retraction = ApproximateInverseRetraction(ExponentialRetraction())
            inverse_retract!(G, X, e, pinvq, inverse_retraction; X0 = X)
        else
            # compute the equivalent logarithm on GL(dim(ğ”½) * n, â„)
            Gáµ£ = GeneralLinear(real_dimension(ğ”½) * n, â„)
            pinvqáµ£ = realify(pinvq, ğ”½)
            Xáµ£ = realify(X, ğ”½)
            eáµ£ = Identity(Gáµ£, pinvqáµ£)
            log!(Gáµ£, Xáµ£, eáµ£, pinvqáµ£)
            unrealify!(X, Xáµ£, ğ”½, n)
        end
    end
    translate_diff!(G, X, p, e, X, LeftAction())
    return X
end
function log!(::GeneralLinear{1}, X, p, q)
    p1 = p isa Identity ? p : p[1]
    X[1] = log(p1 \ q[1])
    return X
end

manifold_dimension(G::GeneralLinear) = manifold_dimension(decorated_manifold(G))

LinearAlgebra.norm(::GeneralLinear, p, X) = norm(X)

project(::GeneralLinear, p) = p
project(::GeneralLinear, p, X) = X

project!(::GeneralLinear, q, p) = copyto!(q, p)
project!(::GeneralLinear, Y, p, X) = copyto!(Y, X)

sharp!(::GeneralLinear{n}, X::TFVector, p, Î¾::CoTFVector) where {n} = copyto!(X, Î¾)

Base.show(io::IO, ::GeneralLinear{n,ğ”½}) where {n,ğ”½} = print(io, "GeneralLinear($n, $ğ”½)")

translate_diff(::GeneralLinear, p, q, X, ::LeftAction) = X
translate_diff(::GeneralLinear, p, q, X, ::RightAction) = p \ X * p

function translate_diff!(G::GeneralLinear, Y, p, q, X, conv::ActionDirection)
    return copyto!(Y, translate_diff(G, p, q, X, conv))
end

vector_transport_to(::GeneralLinear, p, X, q, ::ParallelTransport) = X

vector_transport_to!(::GeneralLinear, Y, p, X, q, ::ParallelTransport) = copyto!(Y, X)
