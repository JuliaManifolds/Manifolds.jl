@doc raw"""
    InvariantMetric{G<:Metric,D<:ActionDirection} <: Metric

Extend a metric on the Lie algebra of an [`AbstractGroupManifold`](@ref) to the whole group
via translation in the specified direction.

Given a group $\mathcal{G}$ and a left- or right group translation map $Ï„$ on the group, a
metric $g$ is $Ï„$-invariant if it has the inner product

````math
g_p(X, Y) = g_{Ï„_q p}((\mathrm{d}Ï„_q)_p X, (\mathrm{d}Ï„_q)_p Y),
````

for all $p,q âˆˆ \mathcal{G}$ and $X,Y âˆˆ T_p \mathcal{G}$, where $(\mathrm{d}Ï„_q)_p$ is the
differential of translation by $q$ evaluated at $p$ (see [`translate_diff`](@ref)).

`InvariantMetric` constructs an (assumed) $Ï„$-invariant metric by extending the inner
product of a metric $h_e$ on the Lie algebra to the whole group:

````math
g_p(X, Y) = h_e((\mathrm{d}Ï„_p^{-1})_p X, (\mathrm{d}Ï„_p^{-1})_p Y).
````

!!! warning
    The invariance condition is not checked and must be verified for the entire group.
    To verify the condition for a set of points numerically, use
    [`has_approx_invariant_metric`](@ref).

The convenient aliases [`LeftInvariantMetric`](@ref) and [`RightInvariantMetric`](@ref) are
provided.

# Constructor

    InvariantMetric(metric::Metric, conv::ActionDirection = LeftAction())
"""
struct InvariantMetric{G<:Metric,D<:ActionDirection} <: Metric
    metric::G
end

function InvariantMetric(metric, conv=LeftAction())
    return InvariantMetric{typeof(metric),typeof(conv)}(metric)
end

const LeftInvariantMetric{G} = InvariantMetric{G,LeftAction} where {G<:Metric}

"""
    LeftInvariantMetric(metric::Metric)

Alias for a left-[`InvariantMetric`](@ref).
"""
LeftInvariantMetric(metric) = InvariantMetric{typeof(metric),LeftAction}(metric)

const RightInvariantMetric{G} = InvariantMetric{G,RightAction} where {G<:Metric}

"""
    RightInvariantMetric(metric::Metric)

Alias for a right-[`InvariantMetric`](@ref).
"""
RightInvariantMetric(metric) = InvariantMetric{typeof(metric),RightAction}(metric)

@doc raw"""
    has_approx_invariant_metric(
        G::Manifold,
        p,
        X,
        Y,
        qs::AbstractVector,
        conv::ActionDirection = LeftAction();
        kwargs...,
    ) -> Bool

Check whether the metric on the group $\mathcal{G}$ is (approximately) invariant using a set of predefined
points. Namely, for $p âˆˆ \mathcal{G}$, $X,Y âˆˆ T_p \mathcal{G}$, a metric $g$, and a
translation map $Ï„_q$ in the specified direction, check for each $q âˆˆ \mathcal{G}$ that the
following condition holds:

````math
g_p(X, Y) â‰ˆ g_{Ï„_q p}((\mathrm{d}Ï„_q)_p X, (\mathrm{d}Ï„_q)_p Y).
````

This is necessary but not sufficient for invariance.

Optionally, `kwargs` passed to `isapprox` may be provided.
"""
has_approx_invariant_metric(::Manifold, ::Any, ::Any, ::Any, ::Any, ::ActionDirection)
function has_approx_invariant_metric(
    M::Manifold,
    p,
    X,
    Y,
    qs,
    conv::ActionDirection=LeftAction();
    kwargs...,
)
    gpXY = inner(M, p, X, Y)
    for q in qs
        Ï„q = translate(M, q, p, conv)
        dÏ„qX = translate_diff(M, q, p, X, conv)
        dÏ„qY = translate_diff(M, q, p, Y, conv)
        isapprox(gpXY, inner(M, Ï„q, dÏ„qX, dÏ„qY); kwargs...) || return false
    end
    return true
end

direction(::InvariantMetric{G,D}) where {G,D} = D()

function exp!(M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric}, q, p, X) where {ğ”½}
    if has_biinvariant_metric(M)
        conv = direction(metric(M))
        return retract!(base_group(M), q, p, X, GroupExponentialRetraction(conv))
    end
    return invoke(exp!, Tuple{MetricManifold,typeof(q),typeof(p),typeof(X)}, M, q, p, X)
end

"""
    biinvariant_metric_dispatch(G::Manifold) -> Val

Return `Val(true)` if the metric on the manifold is bi-invariant, that is, if the metric
is both left- and right-invariant (see [`invariant_metric_dispatch`](@ref)).
"""
function biinvariant_metric_dispatch(M::Manifold)
    return Val(
        invariant_metric_dispatch(M, LeftAction()) === Val(true) &&
        invariant_metric_dispatch(M, RightAction()) === Val(true),
    )
end

has_biinvariant_metric(M::Manifold) = _extract_val(biinvariant_metric_dispatch(M))

@doc raw"""
    invariant_metric_dispatch(G::Manifold, conv::ActionDirection) -> Val

Return `Val(true)` if the metric on the group $\mathcal{G}$ is invariant under translations
by the specified direction, that is, given a group $\mathcal{G}$, a left- or right group
translation map $Ï„$, and a metric $g_e$ on the Lie algebra, a $Ï„$-invariant metric at
any point $p âˆˆ \mathcal{G}$ is defined as a metric with the inner product

````math
g_p(X, Y) = g_{Ï„_q p}((\mathrm{d}Ï„_q)_p X, (\mathrm{d}Ï„_q)_p Y),
````

for $X,Y âˆˆ T_q \mathcal{G}$ and all $q âˆˆ \mathcal{G}$, where $(\mathrm{d}Ï„_q)_p$ is the
differential of translation by $q$ evaluated at $p$ (see [`translate_diff`](@ref)).
"""
invariant_metric_dispatch(::Manifold, ::ActionDirection)

@decorator_transparent_signature invariant_metric_dispatch(
    M::AbstractDecoratorManifold,
    conv::ActionDirection,
)
function invariant_metric_dispatch(M::MetricManifold, conv::ActionDirection)
    is_default_metric(M) && return invariant_metric_dispatch(M.manifold, conv)
    return Val(false)
end
function invariant_metric_dispatch(
    M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric},
    conv::ActionDirection,
) where {ğ”½}
    direction(metric(M)) === conv && return Val(true)
    return invoke(invariant_metric_dispatch, Tuple{MetricManifold,typeof(conv)}, M, conv)
end
invariant_metric_dispatch(M::Manifold, ::ActionDirection) = Val(false)

function has_invariant_metric(M::Manifold, conv::ActionDirection)
    return _extract_val(invariant_metric_dispatch(M, conv))
end

function inner(M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric}, p, X, Y) where {ğ”½}
    imetric = metric(M)
    conv = direction(imetric)
    N = MetricManifold(M.manifold, imetric.metric)
    Xâ‚‘ = inverse_translate_diff(M, p, p, X, conv)
    Yâ‚‘ = inverse_translate_diff(M, p, p, Y, conv)
    return inner(N, Identity(N, p), Xâ‚‘, Yâ‚‘)
end

function default_metric_dispatch(
    M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric},
) where {ğ”½}
    imetric = metric(M)
    N = MetricManifold(M.manifold, imetric.metric)
    default_metric_dispatch(N) === Val(true) || return Val(false)
    return invariant_metric_dispatch(N, direction(imetric))
end

function log!(M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric}, X, p, q) where {ğ”½}
    if has_biinvariant_metric(M)
        imetric = metric(M)
        conv = direction(imetric)
        return inverse_retract!(
            base_group(M),
            X,
            p,
            q,
            GroupLogarithmicInverseRetraction(conv),
        )
    end
    return invoke(log!, Tuple{MetricManifold,typeof(X),typeof(p),typeof(q)}, M, X, p, q)
end

function LinearAlgebra.norm(
    M::MetricManifold{ğ”½,<:Manifold,<:InvariantMetric},
    p,
    X,
) where {ğ”½}
    imetric = metric(M)
    conv = direction(imetric)
    N = MetricManifold(M.manifold, imetric.metric)
    Xâ‚‘ = inverse_translate_diff(M, p, p, X, conv)
    return norm(N, Identity(N, p), Xâ‚‘)
end

function Base.show(io::IO, metric::LeftInvariantMetric)
    return print(io, "LeftInvariantMetric($(metric.metric))")
end
function Base.show(io::IO, metric::RightInvariantMetric)
    return print(io, "RightInvariantMetric($(metric.metric))")
end
