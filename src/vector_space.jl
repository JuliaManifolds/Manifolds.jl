"""
    VectorSpaceType

Abstract type for tangent spaces, cotangent spaces, their tensor products,
exterior products, etc.

Every vector space `fiber` is supposed to provide:
* a method of constructing vectors,
* basic operations: addition, subtraction, multiplication by a scalar
  and negation (unary minus),
* [`zero_vector!(fiber, X, p)`](@ref) to construct zero vectors at point `p`,
* `allocate(X)` and `allocate(X, T)` for vector `X` and type `T`,
* `copyto!(X, Y)` for vectors `X` and `Y`,
* `number_eltype(v)` for vector `v`,
* [`vector_space_dimension(::VectorBundleFibers{<:typeof(fiber)}) where fiber`](@ref).

Optionally:
* inner product via `inner` (used to provide Riemannian metric on vector
  bundles),
* [`flat`](@ref) and [`sharp`](@ref),
* `norm` (by default uses `inner`),
* [`project`](@ref) (for embedded vector spaces),
* [`representation_size`](@ref) (if support for [`ProductArray`](@ref) is desired),
* broadcasting for basic operations.
"""
abstract type VectorSpaceType{ð”½} end


struct TangentSpaceType{ð”½} <: VectorSpaceType{ð”½} end

TangentSpaceType(ð”½) = TangentSpaceType{ð”½}()

struct CotangentSpaceType{ð”½} <: VectorSpaceType{ð”½} end

CotangentSpaceType(ð”½) = CotangentSpaceType{ð”½}()

const TCoTSpaceType = Union{TangentSpaceType,CotangentSpaceType}

"""
    TensorProductType(spaces::VectorSpaceType...)

Vector space type corresponding to the tensor product of given vector space
types.
"""
struct TensorProductType{ð”½,N,TS<:NTuple{N,VectorSpaceType{ð”½}}} <: VectorSpaceType{ð”½}
    spaces::TS
end

"""
    ScalarSpaceType(ð”½)

Vector space of scalars of type ð”½ (see [`AbstractNumbers`](@ref)).
"""
struct ScalarSpaceType{ð”½} <: VectorSpaceType{ð”½} end

ScalarSpaceType(ð”½) = ScalarSpaceType{ð”½}()

function TensorProductType(spaces::VectorSpaceType{ð”½}...) where {ð”½}
    return TensorProductType{ð”½,length(spaces),typeof(spaces)}(spaces)
end

"""
    AbstractTensorField{ð”½1,TM1<:Manifold,VSIn<:VectorSpaceType,ð”½2,TM2<:Manifold,VSOut<:VectorSpaceType}

An abstract map from vector-valued field over a vector space of type `VSIn` over manifold of
type `TM1` to a vector field over a vector space of type `VSOut` over amnifold of type `TM2`.

!!! note

    Manifold `TM1` doesn't have to be over number system `ð”½1` and manifold `TM2` doesn't
    have to be over number system `ð”½2`
"""
abstract type AbstractTensorField{
    ð”½1,
    TM1<:Manifold,
    VSIn<:VectorSpaceType{ð”½1},
    ð”½2,
    TM2<:Manifold,
    VSOut<:VectorSpaceType{ð”½2},
} end

"""
    apply_operator(op::AbstractTensorField, p, v...)

Apply operator `op` at point `p` to arguments (vectors) `v...`.
"""
function apply_operator(op::AbstractTensorField, p, v...) end

const AbstractScalarValuedField{ð”½,TM,VSIn} = AbstractTensorField{
    ð”½,
    TM,
    VSIn,
    ð”½,
    TM,
    ScalarSpaceType,
} where {ð”½,TM<:Manifold,VSIn<:VectorSpaceType{ð”½}}

"""
    MetricField(M::Manifold)

Multilinear scalar field corresponding to the metric of given manifold `M`.
"""
struct MetricField{ð”½,TM<:Manifold} <: AbstractScalarValuedField{
    ð”½,
    TM,
    TensorProductType{Tuple{TangentSpaceType{ð”½},TangentSpaceType{ð”½}}},
}
    manifold::TM
end

apply_operator(op::MetricField, p, X1, X2) = inner(op.manifold, p, X1, X2)

"""
    AbstractCotangentVectorField{ð”½,TM<:Manifold} <:
        AbstractScalarValuedField{ð”½,TM,TangentSpaceType{ð”½}}

Defines unique representation of cotangent vectors at each point on a manifold.
"""
abstract type AbstractCotangentVectorField{ð”½,TM<:Manifold} <:
              AbstractScalarValuedField{ð”½,TM,TangentSpaceType{ð”½}} end

"""
    RieszRepresenterCotangentVectorField(M::Manifold)

Defines Riesz representer representation of cotangent vectors.
"""
struct RieszRepresenterCotangentVectorField{ð”½,TM<:Manifold} <:
       AbstractCotangentVectorField{ð”½,TM}
    manifold::TM
end

function apply_operator(op::RieszRepresenterCotangentVectorField, p, X1, X2)
    return inner(op.manifold, p, X1, X2)
end

"""
    get_cotangent_operator(M::Manifold)

Get the default representation of cotangent vectors for manifold `M`. Defaults to
[`RieszRepresenterCotangentVectorField`](@ref).
"""
function get_cotangent_operator(M::Manifold)
    return RieszRepresenterCotangentVectorField(M)
end

"""
    PushforwardField(manifold_in::Manifold, manifold_out::Manifold, f)

Pushforward of function `f` taking arguments in `manifold_in` and values in `manifold_out`.
"""
struct PushforwardField{ð”½1,TM1<:Manifold,ð”½2,TM2<:Manifold,TF} <:
       AbstractTensorField{ð”½1,TM1,TangentSpaceType{ð”½1},ð”½2,TM2,TangentSpaceType{ð”½2}}
    manifold_in::TM1
    manifold_out::TM2
    f::TF
end

# TODO: get_coordinates for `PushforwardField`

function apply_operator(op::PushforwardField, p, X)
    # TODO
end
