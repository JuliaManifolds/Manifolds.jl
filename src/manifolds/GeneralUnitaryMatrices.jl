@doc """
    AbstractMatrixType

A plain type to distinguish different types of matrices, for example [`DeterminantOneMatrices`](@ref)
and [`AbsoluteDeterminantOneMatrices`](@ref)
"""
abstract type AbstractMatrixType end

@doc """
    DeterminantOneMatrices <: AbstractMatrixType

A type to indicate that we require special (orthogonal / unitary) matrices, i.e. of determinant 1.
"""
struct DeterminantOneMatrices <: AbstractMatrixType end

@doc """
    AbsoluteDeterminantOneMatrices <: AbstractMatrixType

A type to indicate that we require (orthogonal / unitary) matrices with normed determinant,
i.e. that the absolute value of the determinant is 1.
"""
struct AbsoluteDeterminantOneMatrices <: AbstractMatrixType end

@doc raw"""
    GeneralUnitaryMatrices{n,ð”½,S<:AbstractMatrixType} <: AbstractDecoratorManifold

A common parametric type for matrices with a unitary property of size ``nÃ—n`` over the field ``\mathbb F``
which additionally have the `AbstractMatrixType`, e.g. are `DeterminantOneMatrices`.
"""
struct GeneralUnitaryMatrices{n,ð”½,S<:AbstractMatrixType} <: AbstractDecoratorManifold{ð”½} end

function active_traits(f, ::GeneralUnitaryMatrices, args...)
    return merge_traits(IsEmbeddedManifold(), IsDefaultMetric(EuclideanMetric()))
end

@doc raw"""
    check_point(M::UnitaryMatrices, p; kwargs...)
    check_point(M::OrthogonalMatrices, p; kwargs...)
    check_point(M::GeneralUnitaryMatrices{n,ð”½}, p; kwargs...)

Check whether `p` is a valid point on the [`UnitaryMatrices`](@ref) or [`OrthogonalMatrices`] `M`,
i.e. that ``p`` has an determinante of absolute value one

The tolerance for the last test can be set using the `kwargs...`.
"""
function check_point(
    M::GeneralUnitaryMatrices{n,ð”½,AbsoluteDeterminantOneMatrices},
    p;
    kwargs...,
) where {n,ð”½}
    if !isapprox(abs(det(p)), 1; kwargs...)
        return DomainError(
            abs(det(p)),
            "The absolute value of the determinant of $p has to be 1 but it is $(abs(det(p)))",
        )
    end
    if !isapprox(p' * p, one(p); kwargs...)
        return DomainError(
            norm(p' * p - one(p)),
            "$p must be orthogonal but it's not at kwargs $kwargs",
        )
    end
    return nothing
end

@doc raw"""
    check_point(M::Rotations, p; kwargs...)

Check whether `p` is a valid point on the [`UnitaryMatrices`](@ref) `M`,
i.e. that ``p`` has an determinante of absolute value one, i.e. that ``p^{\mathrm{H}}p``

The tolerance for the last test can be set using the `kwargs...`.
"""
function check_point(
    M::GeneralUnitaryMatrices{n,ð”½,DeterminantOneMatrices},
    p;
    kwargs...,
) where {n,ð”½}
    if !isapprox(det(p), 1; kwargs...)
        return DomainError(det(p), "The determinant of $p has to be +1 but it is $(det(p))")
    end
    if !isapprox(p' * p, one(p); kwargs...)
        return DomainError(
            norm(p' * p - one(p)),
            "$p must be orthogonal but it's not at kwargs $kwargs",
        )
    end
    return nothing
end

function check_size(::GeneralUnitaryMatrices{n}, p) where {n}
    m = size(p)
    if length(m) != 2
        return DomainError(
            size(p),
            "The point $p is not a matrix (expected a length of size to be 2, got $(length(size(p))))",
        )
    end
    if m != (n, n)
        return DomainError(
            size(p),
            "The point $p is not a matrix of size $((n,n)), but $(size(p)).",
        )
    end
    return nothing
end
function check_size(::GeneralUnitaryMatrices{n}, p, X) where {n}
    m = size(X)
    if length(size(X)) != 2
        return DomainError(
            size(X),
            "The tangent vector $X is not a matrix (expected a length of size to be 2, got $(length(size(X))))",
        )
    end
    if m != (n, n)
        return DomainError(
            size(X),
            "The tangent vector $X is not a matrix of size $((n,n)), but $(size(X)).",
        )
    end
    return nothing
end

@doc raw"""
    check_vector(M::UnitaryMatrices{n}, p, X; kwargs... )
    check_vector(M::OrthogonalMatrices{n}, p, X; kwargs... )
    check_vector(M::Rotations{n}, p, X; kwargs... )
    check_vector(M::GeneralUnitaryMatrices{n,ð”½}, p, X; kwargs... )

Check whether `X` is a tangent vector to `p` on the [`UnitaryMatrices`](@ref)
space `M`, i.e. after [`check_point`](@ref)`(M,p)`,
``p^{-1}X`` has to be skew symmetric (Hermitian).

The tolerance for the last test can be set using the `kwargs...`.
"""
function check_vector(M::GeneralUnitaryMatrices{n,ð”½}, p, X; kwargs...) where {n,ð”½}
    return check_point(SkewHermitianMatrices(n, ð”½), transpose(p) * X; kwargs...)
end

@doc raw"""
    cos_angles_4d_rotation_matrix(R)

4D rotations can be described by two orthogonal planes that are unchanged by
the action of the rotation (vectors within a plane rotate only within the
plane). The cosines of the two angles $Î±,Î²$ of rotation about these planes may be
obtained from the distinct real parts of the eigenvalues of the rotation
matrix. This function computes these more efficiently by solving the system

```math
\begin{aligned}
\cos Î± + \cos Î² &= \frac{1}{2} \operatorname{tr}(R)\\
\cos Î± \cos Î² &= \frac{1}{8} \operatorname{tr}(R)^2
                 - \frac{1}{16} \operatorname{tr}((R - R^T)^2) - 1.
\end{aligned}
```

By convention, the returned values are sorted in decreasing order.
See also [`angles_4d_skew_sym_matrix`](@ref).
"""
function cos_angles_4d_rotation_matrix(R)
    a = tr(R)
    b = sqrt(clamp(2 * dot(transpose(R), R) - a^2 + 8, 0, Inf))
    return ((a + b) / 4, (a - b) / 4)
end

function default_estimation_method(
    ::GeneralUnitaryMatrices{n,â„},
    ::typeof(mean),
) where {n,ð”½}
    return GeodesicInterpolationWithinRadius(Ï€ / 2 / âˆš2)
end

embed(::GeneralUnitaryMatrices, p) = p

@doc raw"""
    embed(M::GeneralUnitaryMatrices{n,ð”½}, p, X)

Embed the tangent vector `X` at point `p` in `M` is assumed to be of the form
``X = pY``, where  ``Y``is skew symmetric, so the embedding is the identity.
```
"""
embed(::GeneralUnitaryMatrices, p, X)

function embed!(G::GeneralUnitaryMatrices, Y, p, X)
    return copyto!(G, Y, p, X)
end

function exp!(M::GeneralUnitaryMatrices{n,ð”½,S}, q, p, X) where {n,ð”½,S}
    return exp!(GeneralUnitaryMultiplicationGroup{n,ð”½,S}(M), q, p, adjoint(p) * X)
end

@doc raw"""
    get_coordinates(M::Rotations, p, X)
    get_coordinates(M::OrthogonalMatrices, p, X)
    get_coordinates(M::UnitaryMatrices, p, X)

Extract the unique tangent vector components $X^i$ at point `p` on [`Rotations`](@ref)
$\mathrm{SO}(n)$ from the matrix representation `X` of the tangent
vector.

The basis on the Lie algebra $ð”°ð”¬(n)$ is chosen such that
for $\mathrm{SO}(2)$, $X^1 = Î¸ = X_{21}$ is the angle of rotation, and
for $\mathrm{SO}(3)$, $(X^1, X^2, X^3) = (X_{32}, X_{13}, X_{21}) = Î¸ u$ is the
angular velocity and axis-angle representation, where $u$ is the unit vector
along the axis of rotation.

For $\mathrm{SO}(n)$ where $n â‰¥ 4$, the additional elements of $X^i$ are
$X^{j (j - 3)/2 + k + 1} = X_{jk}$, for $j âˆˆ [4,n], k âˆˆ [1,j)$.
"""
get_coordinates(::GeneralUnitaryMatrices{n,â„}, ::Any...) where {n}
function get_coordinates(
    ::GeneralUnitaryMatrices{2,â„},
    p,
    X,
    ::DefaultOrthogonalBasis{â„,TangentSpaceType},
)
    return [X[2]]
end
function get_coordinates(
    ::GeneralUnitaryMatrices{2,â„},
    p::SMatrix,
    X::SMatrix,
    ::DefaultOrthogonalBasis{â„,TangentSpaceType},
)
    return SA[X[2]]
end

function get_coordinates_orthogonal(M::GeneralUnitaryMatrices{n,â„}, p, X, N) where {n}
    Y = allocate_result(M, get_coordinates, p, X, DefaultOrthogonalBasis(N))
    return get_coordinates_orthogonal!(M, Y, p, X, N)
end

function get_coordinates_orthogonal!(::GeneralUnitaryMatrices{2,â„}, Xâ±, p, X, ::RealNumbers)
    Xâ±[1] = X[2]
    return Xâ±
end
function get_coordinates_orthogonal!(
    ::GeneralUnitaryMatrices{n,â„},
    Xâ±,
    p,
    X,
    ::RealNumbers,
) where {n}
    @inbounds begin
        Xâ±[1] = X[3, 2]
        Xâ±[2] = X[1, 3]
        Xâ±[3] = X[2, 1]

        k = 4
        for i in 4:n, j in 1:(i - 1)
            Xâ±[k] = X[i, j]
            k += 1
        end
    end
    return Xâ±
end
function get_coordinates_orthonormal!(
    M::GeneralUnitaryMatrices{n,â„},
    Xâ±,
    p,
    X,
    num::RealNumbers,
) where {n}
    T = Base.promote_eltype(p, X)
    get_coordinates_orthogonal!(M, Xâ±, p, X, num)
    Xâ± .*= sqrt(T(2))
    return Xâ±
end

@doc raw"""
    get_embedding(M::OrthogonalMatrices{n})
    get_embedding(M::Rotations{n})
    get_embedding(M::UnitaryMatrices{n})

Return the embedding, i.e. The ``\mathbb F^{nÃ—n}``, where ``\mathbb F = \mathbb R`` for the
first two and ``\mathbb F = \mathbb C`` for the unitary matrices.
"""
get_embedding(::GeneralUnitaryMatrices{n,ð”½}) where {n,ð”½} = Euclidean(n, n; field=ð”½)

@doc raw"""
    get_vector(M::OrthogonalMatrices, p, Xâ±, B::DefaultOrthogonalBasis)
    get_vector(M::Rotations, p, Xâ±, B::DefaultOrthogonalBasis)

Convert the unique tangent vector components `Xâ±` at point `p` on [`Rotations`](@ref)
or [`OrthogonalMatrices`](@ref)
to the matrix representation $X$ of the tangent vector. See
[`get_coordinates`](@ref get_coordinates(::GeneralUnitaryMatrices{n,â„} where {n}, ::Any...)) for the conventions used.
"""
get_vector(::GeneralUnitaryMatrices{n,â„}, ::Any...) where {n}

function get_vector_orthogonal(
    M::GeneralUnitaryMatrices{n,â„},
    p,
    c,
    N::RealNumbers,
) where {n}
    Y = allocate_result(M, get_vector, p, c)
    return get_vector_orthogonal!(M, Y, p, c, N)
end

function get_vector_orthogonal(::GeneralUnitaryMatrices{2,â„}, p::SMatrix, Xâ±, ::RealNumbers)
    return @SMatrix [0 -Xâ±[]; Xâ±[] 0]
end

function get_vector_orthogonal!(M::GeneralUnitaryMatrices{2,â„}, X, p, Xâ±, N::RealNumbers)
    return get_vector_orthogonal!(M, X, p, Xâ±[1], N)
end
function get_vector_orthogonal!(
    ::GeneralUnitaryMatrices{2,â„},
    X,
    p,
    Xâ±::Real,
    ::RealNumbers,
)
    @assert length(X) == 4
    @inbounds begin
        X[1] = 0
        X[2] = Xâ±
        X[3] = -Xâ±
        X[4] = 0
    end
    return X
end
function get_vector_orthogonal!(
    M::GeneralUnitaryMatrices{n,â„},
    X,
    p,
    Xâ±,
    ::RealNumbers,
) where {n}
    @assert size(X) == (n, n)
    @assert length(Xâ±) == manifold_dimension(M)
    @inbounds begin
        X[1, 1] = 0
        X[1, 2] = -Xâ±[3]
        X[1, 3] = Xâ±[2]
        X[2, 1] = Xâ±[3]
        X[2, 2] = 0
        X[2, 3] = -Xâ±[1]
        X[3, 1] = -Xâ±[2]
        X[3, 2] = Xâ±[1]
        X[3, 3] = 0
        k = 4
        for i in 4:n
            for j in 1:(i - 1)
                X[i, j] = Xâ±[k]
                X[j, i] = -Xâ±[k]
                k += 1
            end
            X[i, i] = 0
        end
    end
    return X
end
function get_vector_orthonormal(
    M::GeneralUnitaryMatrices{n,â„},
    p,
    Xâ±,
    N::RealNumbers,
) where {n}
    return get_vector_orthogonal(M, p, Xâ±, N) ./ sqrt(eltype(Xâ±)(2))
end

function get_vector_orthonormal!(
    M::GeneralUnitaryMatrices{n,â„},
    X,
    p,
    Xâ±,
    N::RealNumbers,
) where {n}
    T = Base.promote_eltype(p, X)
    get_vector_orthogonal!(M, X, p, Xâ±, N)
    X ./= sqrt(T(2))
    return X
end

@doc raw"""
    injectivity_radius(G::Orthogonal)
    injectivity_radius(M::Rotations)

Return the injectivity radius on the [`Rotations`](@ref) and the [`OrthogonalMatrices`](@ref) `M`,
which is globally

````math
    \operatorname{inj}_{\mathrm{O}(n)}(p) = \operatorname{inj}_{\mathrm{SO}(n)}(p) = Ï€\sqrt{2}.
````
"""
injectivity_radius(::GeneralUnitaryMatrices{n,â„}) where {n} = Ï€ * sqrt(2.0)
function _injectivity_radius(
    ::GeneralUnitaryMatrices{n,â„},
    ::ExponentialRetraction,
) where {n}
    return Ï€ * sqrt(2.0)
end
function _injectivity_radius(::GeneralUnitaryMatrices{n,â„}, ::PolarRetraction) where {n}
    return Ï€ / sqrt(2.0)
end
@doc raw"""
    injectivity_radius(G::UnitaryMatrices)

Return the injectivity radius on the [`UnitaryMatrices`](@ref), which is ``Ï€``.
"""
injectivity_radius(::GeneralUnitaryMatrices{n,â„‚}) where {n} = Ï€

inner(::GeneralUnitaryMatrices, p, X, Y) = dot(X, Y)

@doc raw"""
    log(M::Rotations, p, X)
    log(M::OrthogonalMatrices, p, X)
    log(M::UnitaryMatrices, p, X)

Compute the logarithmic map, that is,

```
log_p q = p\log(p^{\mathrm{H}q)
```
"""
log(::GeneralUnitaryMatrices, p, q)

function ManifoldsBase.log(M::GeneralUnitaryMatrices{2,â„,S}, p, q) where {S}
    return p * log(GeneralUnitaryMultiplicationGroup{2,â„,S}(M), p, q)
end
function log!(M::GeneralUnitaryMatrices{n,ð”½,S}, X, p, q) where {n,ð”½,S}
    log!(GeneralUnitaryMultiplicationGroup{n,ð”½,S}(M), X, p, q)
    X .= p * X
    return X
end

norm(::GeneralUnitaryMatrices, p, X) = norm(adjoint(p) * X)

@doc raw"""
    manifold_dimension(M::Rotations)
    manifold_dimension(M::OrthogonalMatrices)

Return the dimension of the manifold orthogonal matrices and of the manifold of rotations
```math
\dim_{\mathrm{O}(n)} = \dim_{\mathrm{SO}(n)} = \frac{n(n-1)}{2}.
```
"""
manifold_dimension(::GeneralUnitaryMatrices{n,â„}) where {n} = div(n * (n - 1), 2)

@doc raw"""
    manifold_dimension(M::GeneralUnitaryMatrices{n,â„‚,DeterminantOneMatrices})

Return the dimension of the manifold of special unitary matrices.
```math
\dim_{\mathrm{SU}(n)} = n^2-1.
```
"""
manifold_dimension(::GeneralUnitaryMatrices{n,â„‚,DeterminantOneMatrices}) where {n} = n^2 - 1

"""
    mean(
        M::Rotations,
        x::AbstractVector,
        [w::AbstractWeights,]
        method = GeodesicInterpolationWithinRadius(Ï€/2/âˆš2);
        kwargs...,
    )

Compute the Riemannian [`mean`](@ref mean(M::AbstractManifold, args...)) of `x` using
[`GeodesicInterpolationWithinRadius`](@ref).
"""
mean(::GeneralUnitaryMatrices{n,â„}, ::Any) where {n}

@doc raw"""
     project(G::UnitaryMatrices{n}, p)
     project(G::OrthogonalMatrices{n}, p)

Project the point ``p âˆˆ ð”½^{n Ã— n}`` to the nearest point in
``\mathrm{U}(n,ð”½)=``[`Unitary(n,ð”½)`](@ref) under the Frobenius norm.
If ``p = U S V^\mathrm{H}`` is the singular value decomposition of ``p``, then the projection
is

````math
  \operatorname{proj}_{\mathrm{U}(n,ð”½)} \colon p â†¦ U V^\mathrm{H}.
````
"""
project(::GeneralUnitaryMatrices{n,ð”½,AbsoluteDeterminantOneMatrices}, p) where {n,ð”½}

function project!(
    ::GeneralUnitaryMatrices{n,ð”½,AbsoluteDeterminantOneMatrices},
    q,
    p,
) where {n,ð”½}
    F = svd(p)
    mul!(q, F.U, F.Vt)
    return q
end

@doc raw"""
     project(M::OrthogonalMatrices{n}, p, X)
     project(M::Rotations{n}, p, X)
     project(M::UnitaryMatrices{n}, p, X)

Orthogonally project the tangent vector ``X âˆˆ ð”½^{n Ã— n}``, ``\mathbb F âˆˆ \{\mathbb R, \mathbb C\}``
to the tangent space of `M` at `p`,
and change the representer to use the corresponding Lie algebra, i.e. we compute

```math
    \operatorname{proj}_p(X) = p\frac{p^{-1}X-(p^{-1}X)^{\mathrm{T}}}{2},
```
"""
project(::GeneralUnitaryMatrices, p, X)

function project!(::GeneralUnitaryMatrices{n,ð”½}, Y, p, X) where {n,ð”½}
    project!(SkewHermitianMatrices(n, ð”½), Y, p \ X)
    Y .= p * Y
    return Y
end

@doc raw"""
    retract(M::Rotations, p, X, ::PolarRetraction)
    retract(M::OrthogonalMatrices, p, X, ::PolarRetraction)

Compute the SVD-based retraction on the [`Rotations`](@ref) and [`OrthogonalMatrices`](@ref) `M` from `p` in direction `X`
(as an element of the Lie group) and is a second-order approximation of the exponential map.
Let

````math
USV = p + pX
````

be the singular value decomposition, then the formula reads

````math
\operatorname{retr}_p X = UV^\mathrm{T}.
````
"""
retract(::GeneralUnitaryMatrices{n,ð”½}, ::Any, ::Any, ::PolarRetraction) where {n,ð”½}

@doc raw"""
    retract(M::Rotations, p, X, ::QRRetraction)
    retract(M::OrthogonalMatrices, p. X, ::QRRetraction)

Compute the QR-based retraction on the [`Rotations`](@ref) and [`OrthogonalMatrices`](@ref) `M` from `p` in direction `X`
(as an element of the Lie group), which is a first-order approximation of the exponential map.

This is also the default retraction on these manifolds.
"""
retract(::GeneralUnitaryMatrices{n,ð”½}, ::Any, ::Any, ::QRRetraction) where {n,ð”½}

function retract_qr!(::GeneralUnitaryMatrices{n,ð”½}, q::AbstractArray{T}, p, X) where {n,ð”½,T}
    A = p + X
    qr_decomp = qr(A)
    d = diag(qr_decomp.R)
    D = Diagonal(sign.(d .+ convert(T, 0.5)))
    return copyto!(q, qr_decomp.Q * D)
end
function retract_polar!(M::GeneralUnitaryMatrices{n,ð”½}, q, p, X) where {n,ð”½}
    A = p + X
    return project!(M, q, A; check_det=false)
end

@doc raw"""
    riemann_tensor(::GeneralUnitaryMatrices, p, X, Y, Z)

Compute the value of Riemann tensor on the [`GeneralUnitaryMatrices`](@ref) manifold.
The formula reads[^Rentmeesters2011] ``R(X,Y)Z=\frac{1}{4}[Z, [X, Y]]``.

[^Rentmeesters2011]:
    > Q. Rentmeesters, â€œA gradient method for geodesic data fitting on some symmetric
    > Riemannian manifolds,â€ in 2011 50th IEEE Conference on Decision and Control and
    > European Control Conference, Dec. 2011, pp. 7141â€“7146. doi: 10.1109/CDC.2011.6161280.
"""
riemann_tensor(::GeneralUnitaryMatrices, p, X, Y, Z)

function riemann_tensor!(::GeneralUnitaryMatrices, Xresult, p, X, Y, Z)
    Xtmp = X * Y - Y * X
    Xresult .= 1 // 4 .* (Z * Xtmp .- Xtmp * Z)
    return Xresult
end
