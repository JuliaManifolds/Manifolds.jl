@doc raw"""
    SphereSymmetricMatrices{n,ğ”½} <: AbstractEmbeddedManifold{â„,TransparentIsometricEmbedding}

The [`Manifold`](@ref) consisting of the $n Ã— n$ symmetric matrices
of unit Frobenius norm, i.e.
````math
\mathcal{S}_{\text{sym}} :=\bigl\{p  âˆˆ ğ”½^{n Ã— n}\ \big|\ p^{\mathrm{H}} = p, \lVert p \rVert = 1 \bigr\},
````
where $\cdot^{\mathrm{H}}$ denotes the Hermitian, i.e. complex conjugate transpose,
and the field $ğ”½ âˆˆ \{ â„, â„‚\}$.

# Constructor
    SphereSymmetricMatrices(n[, field=â„])

Generate the manifold of `n`-by-`n` symmetric matrices of unit Frobenius norm.
"""
struct SphereSymmetricMatrices{N,ğ”½} <:
       AbstractEmbeddedManifold{ğ”½,TransparentIsometricEmbedding} end

function SphereSymmetricMatrices(n::Int, field::AbstractNumbers=â„)
    return SphereSymmetricMatrices{n,field}()
end

@doc raw"""
    check_manifold_point(M::SphereSymmetricMatrices{n,ğ”½}, p; kwargs...)

Check whether the matrix is a valid point on the [`SphereSymmetricMatrices`](@ref) `M`,
i.e. is an `n`-by-`n` symmetric matrix of unit Frobenius norm.

The tolerance for the symmetry of `p` can be set using `kwargs...`.
"""
function check_manifold_point(M::SphereSymmetricMatrices{n,ğ”½}, p; kwargs...) where {n,ğ”½}
    mpv =
        invoke(check_manifold_point, Tuple{supertype(typeof(M)),typeof(p)}, M, p; kwargs...)
    mpv === nothing || return mpv
    if !isapprox(norm(p - p'), 0.0; kwargs...)
        return DomainError(
            norm(p - p'),
            "The point $(p) does not lie on $M, since it is not symmetric.",
        )
    end
    return nothing
end

"""
    check_tangent_vector(M::SphereSymmetricMatrices{n,ğ”½}, p, X; check_base_point = true, kwargs... )

Check whether `X` is a tangent vector to manifold point `p` on the
[`SphereSymmetricMatrices`](@ref) `M`, i.e. `X` has to be a symmetric matrix of size `(n,n)`
of unit Frobenius norm.
The optional parameter `check_base_point` indicates, whether to call
 [`check_manifold_point`](@ref)  for `p`.

The tolerance for the symmetry of `p` and `X` can be set using `kwargs...`.
"""
function check_tangent_vector(
    M::SphereSymmetricMatrices{n,ğ”½},
    p,
    X;
    check_base_point=true,
    kwargs...,
) where {n,ğ”½}
    if check_base_point
        mpe = check_manifold_point(M, p; kwargs...)
        mpe === nothing || return mpe
    end
    mpv = invoke(
        check_tangent_vector,
        Tuple{supertype(typeof(M)),typeof(p),typeof(X)},
        M,
        p,
        X;
        check_base_point=false, # already checked above
        kwargs...,
    )
    mpv === nothing || return mpv
    if !isapprox(norm(X - X'), 0.0; kwargs...)
        return DomainError(
            norm(X - X'),
            "The vector $(X) is not a tangent vector to $(p) on $(M), since it is not symmetric.",
        )
    end
    return nothing
end

function decorated_manifold(M::SphereSymmetricMatrices{n,ğ”½}) where {n,ğ”½}
    return ArraySphere(n, n; field=ğ”½)
end

@doc raw"""
    manifold_dimension(M::SphereSymmetricMatrices{n,ğ”½})

Return the manifold dimension of the [`SphereSymmetricMatrices`](@ref) `n`-by-`n` symmetric matrix `M` of unit
Frobenius norm over the number system `ğ”½`, i.e.

````math
\begin{aligned}
\dim(\mathcal{S}_{\text{sym}})(n,â„) &= \frac{n(n+1)}{2} - 1,\\
\dim(\mathcal{S}_{\text{sym}})(n,â„‚) &= 2\frac{n(n+1)}{2} - n -1.
\end{aligned}
````
"""
function manifold_dimension(::SphereSymmetricMatrices{n,ğ”½}) where {n,ğ”½}
    return div(n * (n + 1), 2) * real_dimension(ğ”½) - (ğ”½ === â„‚ ? n : 0) - 1
end

@doc raw"""
    project(M::SphereSymmetricMatrices, p)

Projects `p` from the embedding onto the [`SphereSymmetricMatrices`](@ref) `M`, i.e.

````math
\operatorname{proj}_{\mathcal{S}_{\text{sym}}}(p) = \frac{1}{2} \bigl( p + p^{\mathrm{H}} \bigr),
````
where $\cdot^{\mathrm{H}}$ denotes the Hermitian, i.e. complex conjugate transposed.
"""
project(::SphereSymmetricMatrices, ::Any)

function project!(M::SphereSymmetricMatrices, q, p)
    return project!(get_embedding(M), q, (p + p') ./ 2)
end

@doc raw"""
    project(M::SphereSymmetricMatrices, p, X)

Project the matrix `X` onto the tangent space at `p` on the [`SphereSymmetricMatrices`](@ref) `M`, i.e.

````math
\operatorname{proj}_p(X) = \frac{X + X^{\mathrm{H}}}{2} - âŸ¨p, \frac{X + X^{\mathrm{H}}}{2}âŸ©p,
````
where $\cdot^{\mathrm{H}}$ denotes the Hermitian, i.e. complex conjugate transposed.
"""
project(::SphereSymmetricMatrices, ::Any, ::Any)

function project!(M::SphereSymmetricMatrices, Y, p, X)
    return project!(get_embedding(M), Y, p, (X .+ X') ./ 2)
end

@generated representation_size(::SphereSymmetricMatrices{n,ğ”½}) where {n,ğ”½} = (n, n)

function Base.show(io::IO, ::SphereSymmetricMatrices{n,ğ”½}) where {n,ğ”½}
    return print(io, "SphereSymmetricMatrices($(n), $(ğ”½))")
end
