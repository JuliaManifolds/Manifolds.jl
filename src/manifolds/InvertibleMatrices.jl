@doc raw"""
    InvertibleMatrices{ğ”½,T} <: AbstractDecoratorManifold{ğ”½}

The [`AbstractManifold`](@extref `ManifoldsBase.AbstractManifold`)
consisting of the real- or complex-valued invertible matrices, that is the set

```math
\bigl\{p  âˆˆ ğ”½^{nÃ—n}\ \big|\ \det(p) \neq 0 \bigr\},
```
where the field ``ğ”½ âˆˆ \{ â„, â„‚\}``.

# Constructor

    InvertibleMatrices(n::Int, field::AbstractNumbers=â„)

Generate the manifold of ``nÃ—n`` invertible matrices.
"""
struct InvertibleMatrices{ğ”½,T} <: AbstractDecoratorManifold{ğ”½}
    size::T
end

function InvertibleMatrices(n::Int, field::AbstractNumbers=â„; parameter::Symbol=:type)
    size = wrap_type_parameter(parameter, (n,))
    return InvertibleMatrices{field,typeof(size)}(size)
end

function active_traits(f, ::InvertibleMatrices, args...)
    return merge_traits(IsEmbeddedSubmanifold())
end

@doc raw"""
    check_point(M::InvertibleMatrices{n,ğ”½}, p; kwargs...)

Check whether `p` is a valid manifold point on the [`InvertibleMatrices`](@ref) `M`, i.e.
whether `p` is an invertible matrix of size `(n,n)` with values from the corresponding
[`AbstractNumbers`](@extref ManifoldsBase number-system) `ğ”½`.
"""
function check_point(M::InvertibleMatrices, p; kwargs...)
    if det(p) == 0
        return DomainError(
            det(p),
            "The point $(p) does not lie on $(M), since its determinant is zero and hence it is not invertible.",
        )
    end
    return nothing
end

"""
    check_vector(M::InvertibleMatrices{n,ğ”½}, p, X; kwargs... )

Check whether `X` is a tangent vector to manifold point `p` on the
[`InvertibleMatrices`](@ref) `M`, which are all matrices of size ``nÃ—n``
its values have to be from the correct [`AbstractNumbers`](@extref ManifoldsBase number-system).
"""
function check_vector(M::InvertibleMatrices, p, X; kwargs...)
    return nothing
end

embed(::InvertibleMatrices, p) = p
embed(::InvertibleMatrices, p, X) = X

function get_coordinates(
    ::InvertibleMatrices{â„,<:Any},
    p,
    X,
    ::DefaultOrthonormalBasis{â„,TangentSpaceType},
)
    return vec(X)
end

function get_coordinates!(
    ::InvertibleMatrices{â„,<:Any},
    Xâ±,
    p,
    X,
    ::DefaultOrthonormalBasis{â„,TangentSpaceType},
)
    return copyto!(Xâ±, X)
end

function get_embedding(::InvertibleMatrices{ğ”½,TypeParameter{Tuple{n}}}) where {n,ğ”½}
    return Euclidean(n, n; field=ğ”½)
end
function get_embedding(M::InvertibleMatrices{ğ”½,Tuple{Int}}) where {ğ”½}
    n = get_parameter(M.size)[1]
    return Euclidean(n, n; field=ğ”½, parameter=:field)
end

function get_vector(
    M::InvertibleMatrices{â„,<:Any},
    p,
    Xâ±,
    ::DefaultOrthonormalBasis{â„,TangentSpaceType},
)
    n = get_parameter(M.size)[1]
    return reshape(Xâ±, n, n)
end

function get_vector!(
    ::InvertibleMatrices{â„,<:Any},
    X,
    p,
    Xâ±,
    ::DefaultOrthonormalBasis{â„,TangentSpaceType},
)
    return copyto!(X, Xâ±)
end

"""
    is_flat(::InvertibleMatrices)

Return true. [`InvertibleMatrices`](@ref) is a flat manifold.
"""
is_flat(M::InvertibleMatrices) = true

@doc raw"""
    manifold_dimension(M::InvertibleMatrices{n,ğ”½})

Return the dimension of the [`InvertibleMatrices`](@ref) matrix `M` over the number system
`ğ”½`, which is the same dimension as its embedding, the [`Euclidean`](@ref)`(n, n; field=ğ”½)`.
"""
function manifold_dimension(M::InvertibleMatrices{<:Any,ğ”½}) where {ğ”½}
    return manifold_dimension(get_embedding(M))
end

@doc raw"""
    Random.rand(M::InvertibleMatrices; vector_at=nothing, kwargs...)

If `vector_at` is `nothing`, return a random point on the [`InvertibleMatrices`](@ref)
manifold `M` by using `rand` in the embedding.

If `vector_at` is not `nothing`, return a random tangent vector from the tangent space of
the point `vector_at` on the [`InvertibleMatrices`](@ref) by using by using `rand` in the
embedding.
"""
rand(M::InvertibleMatrices; kwargs...)

function Random.rand!(M::InvertibleMatrices, pX; kwargs...)
    rand!(get_embedding(M), pX; kwargs...)
    while det(pX) == 0
        rand!(get_embedding(M), pX; kwargs...)
    end
    return pX
end
function Random.rand!(rng::AbstractRNG, M::InvertibleMatrices, pX; kwargs...)
    rand!(rng, get_embedding(M), pX; kwargs...)
    while det(pX) == 0
        rand!(rng, get_embedding(M), pX; kwargs...)
    end
    return pX
end

function Base.show(io::IO, ::InvertibleMatrices{ğ”½,TypeParameter{Tuple{n}}}) where {n,ğ”½}
    return print(io, "InvertibleMatrices($(n), $(ğ”½))")
end
function Base.show(io::IO, M::InvertibleMatrices{ğ”½,Tuple{Int}}) where {ğ”½}
    n = get_parameter(M.size)[1]
    return print(io, "InvertibleMatrices($(n), $(ğ”½); parameter=:field)")
end

@doc raw"""
    Y = Weingarten(M::InvertibleMatrices, p, X, V)
    Weingarten!(M::InvertibleMatrices, Y, p, X, V)

Compute the Weingarten map ``\mathcal W_p`` at `p` on the [`InvertibleMatrices`](@ref) `M` with respect to the
tangent vector ``X \in T_p\mathcal M`` and the normal vector ``V \in N_p\mathcal M``.

Since this a flat space by itself, the result is always the zero tangent vector.
"""
Weingarten(::InvertibleMatrices, p, X, V)

Weingarten!(::InvertibleMatrices, Y, p, X, V) = fill!(Y, 0)
