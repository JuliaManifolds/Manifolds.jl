"""
    AbstractSphere{ùîΩ} <: AbstractEmbeddedManifold{ùîΩ,DefaultIsometricEmbeddingType}

An abstract type to represent a unit sphere that is represented isometrically in the embedding.
"""
abstract type AbstractSphere{ùîΩ} <: AbstractEmbeddedManifold{ùîΩ,DefaultIsometricEmbeddingType} end

@doc raw"""
    Sphere{n,ùîΩ} <: AbstractSphere{ùîΩ}

The (unit) sphere manifold $ùïä^{n}$ is the set of all unit norm vectors in $ùîΩ^{n+1}$.
The sphere is represented in the embedding, i.e.

````math
ùïä^{n} := \bigl\{ p \in ùîΩ^{n+1}\ \big|\ \lVert p \rVert = 1 \bigr\}
````

where $ùîΩ\in\{‚Ñù,‚ÑÇ,‚Ñç\}$. Note that compared to the [`ArraySphere`](@ref), here the
argument `n` of the manifold is the dimension of the manifold, i.e. $ùïä^{n} ‚äÇ ùîΩ^{n+1}$, $n\in ‚Ñï$.

The tangent space at point $p$ is given by

````math
T_pùïä^{n} := \bigl\{ X ‚àà ùîΩ^{n+1}\ |\ \Re(‚ü®p,X‚ü©) = 0 \bigr \},
````

where $ùîΩ\in\{‚Ñù,‚ÑÇ,‚Ñç\}$ and $‚ü®\cdot,\cdot‚ü©$ denotes the inner product in the
embedding $ùîΩ^{n+1}$.

For $ùîΩ=‚ÑÇ$, the manifold is the complex sphere, written $‚ÑÇùïä^n$, embedded in $‚ÑÇ^{n+1}$.
$‚ÑÇùïä^n$ is the complexification of the real sphere $ùïä^{2n+1}$.
Likewise, the quaternionic sphere $‚Ñçùïä^n$ is the quaternionification of the real sphere
$ùïä^{4n+3}$.
Consequently, $‚ÑÇùïä^0$ is equivalent to $ùïä^1$ and [`Circle`](@ref), while $‚ÑÇùïä^1$ and $‚Ñçùïä^0$
are equivalent to $ùïä^3$, though with different default representations.

This manifold is modeled as a special case of the more general case, i.e. as an embedded
manifold to the [`Euclidean`](@ref), and several functions like the [`inner`](@ref inner(::Euclidean, ::Any...)) product
and the [`zero_tangent_vector`](@ref zero_tangent_vector(::Euclidean, ::Any...)) are inherited from the embedding.

# Constructor

    Sphere(n[, field=‚Ñù])

Generate the (real-valued) sphere $ùïä^{n} ‚äÇ ‚Ñù^{n+1}$, where `field` can also be used to
generate the complex- and quaternionic-valued sphere.
"""
struct Sphere{N,ùîΩ} <: AbstractSphere{ùîΩ} end
Sphere(n::Int, field::AbstractNumbers = ‚Ñù) = Sphere{n,field}()


@doc raw"""
    ArraySphere{T<:Tuple,ùîΩ} <: AbstractSphere{ùîΩ}

The (unit) sphere manifold $ùïä^{n‚ÇÅ,n‚ÇÇ,...,n·µ¢}$ is the set of all unit (Frobenius) norm elements of
$ùîΩ^{n‚ÇÅ,n‚ÇÇ,...,n·µ¢}$, where $ùîΩ\in\{‚Ñù,‚ÑÇ,‚Ñç\}. The generalized sphere is
represented in the embedding, and supports arbitrary sized arrays or in other words arbitrary
tensors of unit norm. The set formally reads

````math
ùïä^{n_1, n_2, ‚Ä¶, n_i} := \bigl\{ p \in ùîΩ^{n_1, n_2, ‚Ä¶, n_i}\ \big|\ \lVert p \rVert = 1 \bigr\}
````

where $ùîΩ\in\{‚Ñù,‚ÑÇ,‚Ñç\}$. Setting $i=1$ and $ùîΩ=‚Ñù$  this  simplifies to unit vectors in $‚Ñù^n$, see
[`Sphere`](@ref) for this special case. Note that compared to this classical case,
the argument for the generalized case here is given by the dimension of the embedding.
This means that `Sphere(2)` and `ArraySphere(3)` are the same manifold.

The tangent space at point $p$ is given by

````math
T_p ùïä^{n_1, n_2, ‚Ä¶, n_i} := \bigl\{ X ‚àà ùîΩ^{n_1, n_2, ‚Ä¶, n_i}\ |\ ‚ü®p,X‚ü© = 0 \bigr \},
````

where $ùîΩ\in\{‚Ñù,‚ÑÇ,‚Ñç\}$ and $‚ü®\cdot,\cdot‚ü©$ denotes the inner product in the
embedding $ùîΩ^{n_1, n_2, ‚Ä¶, n_i}$.

This manifold is modeled as an embedded manifold to the [`Euclidean`](@ref), i.e.
several functions like the [`inner`](@ref inner(::Euclidean, ::Any...)) product and the
[`zero_tangent_vector`](@ref zero_tangent_vector(::Euclidean, ::Any...)) are inherited from the embedding.

# Constructor

    ArraySphere(n‚ÇÅ,n‚ÇÇ,...,n·µ¢; field=‚Ñù)

Generate sphere in $ùîΩ^{n_1, n_2, ‚Ä¶, n_i}$, where $ùîΩ$ defaults to the real-valued case $‚Ñù$.
"""
struct ArraySphere{N,ùîΩ} <: AbstractSphere{ùîΩ} where {N<:Tuple} end
function ArraySphere(n::Vararg{Int,I}; field::AbstractNumbers = ‚Ñù) where {I}
    return ArraySphere{Tuple{n...},field}()
end

"""
    check_manifold_point(M::AbstractSphere, p; kwargs...)

Check whether `p` is a valid point on the [`AbstractSphere`](@ref) `M`, i.e. is a point in
the embedding of unit length.
The tolerance for the last test can be set using the `kwargs...`.
"""
function check_manifold_point(M::AbstractSphere, p; kwargs...)
    mpv = invoke(
        check_manifold_point,
        Tuple{(typeof(get_embedding(M))),typeof(p)},
        get_embedding(M),
        p;
        kwargs...,
    )
    mpv === nothing || return mpv
    if !isapprox(norm(p), 1.0; kwargs...)
        return DomainError(
            norm(p),
            "The point $(p) does not lie on the $(M) since its norm is not 1.",
        )
    end
    return nothing
end

"""
    check_tangent_vector(M::AbstractSphere, p, X; check_base_point = true, kwargs... )

Check whether `X` is a tangent vector to `p` on the [`AbstractSphere`](@ref) `M`, i.e.
after [`check_manifold_point`](@ref)`(M,p)`, `X` has to be of same dimension as `p`
and orthogonal to `p`.
The optional parameter `check_base_point` indicates, whether to call
[`check_manifold_point`](@ref)  for `p` or not.
The tolerance for the last test can be set using the `kwargs...`.
"""
function check_tangent_vector(M::AbstractSphere, p, X; check_base_point = true, kwargs...)
    if check_base_point
        mpe = check_manifold_point(M, p; kwargs...)
        mpe === nothing || return mpe
    end
    mpv = invoke(
        check_tangent_vector,
        Tuple{typeof(get_embedding(M)),typeof(p),typeof(X)},
        get_embedding(M),
        p,
        X;
        check_base_point = false, # already checked above
        kwargs...,
    )
    mpv === nothing || return mpv
    if !isapprox(abs(real(dot(p, X))), 0.0; kwargs...)
        return DomainError(
            abs(dot(p, X)),
            "The vector $(X) is not a tangent vector to $(p) on $(M), since it is not orthogonal in the embedding.",
        )
    end
    return nothing
end

function decorated_manifold(M::AbstractSphere{ùîΩ}) where {ùîΩ}
    return Euclidean(representation_size(M)...; field = ùîΩ)
end
get_embedding(M::AbstractSphere{ùîΩ}) where {ùîΩ} = decorated_manifold(M)


@doc raw"""
    distance(M::AbstractSphere, p, q)

Compute the geodesic distance betweeen `p` and `q` on the [`AbstractSphere`](@ref) `M`.
The formula is given by the (shorter) great arc length on the (or a) great circle
both `p` and `q` lie on.

````math
d_{ùïä}(p,q) = \arccos(‚ü®p,q‚ü©).
````
"""
distance(::AbstractSphere, p, q) = acos(clamp(real(dot(p, q)), -1, 1))

@doc raw"""
    exp(M::AbstractSphere, p, X)

Compute the exponential map from `p` in the tangent direction `X` on the [`AbstractSphere`](@ref)
`M` by following the great arc eminating from `p` in direction `X`.

````math
\exp_p X = \cos(\lVert X \rVert_p)p + \sin(\lVert X \rVert_p)\frac{X}{\lVert X \rVert_p},
````
where $\lVert X \rVert_p$ is the [`norm`](@ref norm(::AbstractSphere,p,X)) on the
tangent space at `p` of the [`AbstractSphere`](@ref) `M`.
"""
exp(::AbstractSphere, ::Any...)

function exp!(M::AbstractSphere, q, p, X)
    Œ∏ = norm(M, p, X)
    q .= cos(Œ∏) .* p .+ usinc(Œ∏) .* X
    return q
end

flat!(M::AbstractSphere, Œæ::CoTFVector, p, X::TFVector) = copyto!(Œæ, X)

function get_basis(M::Sphere{n,‚Ñù}, p, B::DiagonalizingOrthonormalBasis{‚Ñù}) where {n}
    A = zeros(n + 1, n + 1)
    A[1, :] = transpose(p)
    A[2, :] = transpose(B.frame_direction)
    V = nullspace(A)
    Œ∫ = ones(n)
    if !iszero(B.frame_direction)
        # if we have a nonzero direction for the geodesic, add it and it gets curvature zero from the tensor
        V = cat(B.frame_direction / norm(M, p, B.frame_direction), V; dims = 2)
        Œ∫[1] = 0 # no curvature along the geodesic direction, if x!=y
    end
    Œû = [V[:, i] for i in 1:n]
    return CachedBasis(B, Œ∫, Œû)
end

@doc raw"""
    get_coordinates(M::Sphere, p, X, B::DefaultOrthonormalBasis)

Represent the tangent vector `X` at point `p` from the [`Sphere`](@ref) `M` in
an orthonormal basis by rotating the vector `X` using the rotation matrix
$2\frac{q q^\mathrm{T}}{q^\mathrm{T} q} - I$ where $q = p + (1, 0, ‚Ä¶, 0)$.
"""
get_coordinates(::Sphere{n,‚Ñù}, p, X, B::DefaultOrthonormalBasis) where {n}

function get_coordinates!(
    ::Sphere{n,‚Ñù},
    Y,
    p,
    X,
    ::DefaultOrthonormalBasis,
) where {n}
    factor = X[1] / (1 + p[1])
    Y .= factor .* view(p, 2:(n + 1)) .- view(X, 2:(n + 1))
    return Y
end

# TODO: add docstring
@doc raw"""
"""
get_vector(::Sphere{n,‚Ñù}, p, X, ::DefaultOrthonormalBasis) where {n}

function get_vector!(
    ::Sphere{n,‚Ñù},
    Y::AbstractVector,
    p,
    X,
    ::DefaultOrthonormalBasis,
) where {n}
    pend = view(p, 2:(n + 1))
    pX = dot(pend, X)
    Y[1] = pX
    Y[2:(n + 1)] .= (pX / (1 + p[1])) .* pend .- X
    return Y
end

@doc raw"""
    injectivity_radius(M::AbstractSphere[, p])

Return the injectivity radius for the [`AbstractSphere`](@ref) `M`, which is globally $œÄ$.

    injectivity_radius(M::Sphere, x, ::ProjectionRetraction)

Return the injectivity radius for the [`ProjectionRetraction`](@ref) on the
[`AbstractSphere`](@ref), which is globally $\frac{œÄ}{2}$.
"""
injectivity_radius(::AbstractSphere) = œÄ
injectivity_radius(::AbstractSphere, ::ExponentialRetraction) = œÄ
injectivity_radius(::AbstractSphere, ::ProjectionRetraction) = œÄ / 2
injectivity_radius(::AbstractSphere, ::Any) = œÄ
injectivity_radius(::AbstractSphere, ::Any, ::ExponentialRetraction) = œÄ
injectivity_radius(::AbstractSphere, ::Any, ::ProjectionRetraction) = œÄ / 2
eval(
    quote
        @invoke_maker 1 Manifold injectivity_radius(
            M::AbstractSphere,
            rm::AbstractRetractionMethod,
        )
    end,
)

@doc raw"""
    inverse_retract(M::AbstractSphere, p, q, ::ProjectionInverseRetraction)

Compute the inverse of the projection based retraction on the [`AbstractSphere`](@ref) `M`,
i.e. rearranging $p+X = q\lVert p+X\rVert_2$ yields
since $‚ü®p,X‚ü© = 0$ and when $d_{ùïä^2}(p,q) ‚â§ \frac{œÄ}{2}$ that

````math
\operatorname{retr}_p^{-1}(q) = \frac{q}{‚ü®p, q‚ü©} - p.
````
"""
inverse_retract(::AbstractSphere, ::Any, ::Any, ::ProjectionInverseRetraction)

function inverse_retract!(::AbstractSphere, X, p, q, ::ProjectionInverseRetraction)
    return (X .= q ./ real(dot(p, q)) .- p)
end

@doc raw"""
    log(M::AbstractSphere, p, q)

Compute the logarithmic map on the [`AbstractSphere`](@ref) `M`, i.e. the tangent vector,
whose geodesic starting from `p` reaches `q` after time 1.
The formula reads for $x ‚â† -y$

````math
\log_p q = d_{ùïä}(p,q) \frac{q-‚ü®p,q‚ü© p}{\lVert q-‚ü®p,q‚ü© p \rVert_2},
````

and a deterministic choice from the set of tangent vectors is returned if $x=-y$, i.e. for
opposite points.
"""
log(::AbstractSphere, ::Any...)

function log!(M::AbstractSphere, X, p, q)
    cosŒ∏ = clamp(real(dot(p, q)), -1, 1)
    if cosŒ∏ ‚âà -1 # appr. opposing points, return deterministic choice from set-valued log
        fill!(X, 0)
        if p[1] ‚âà 1
            X[2] = 1
        else
            X[1] = 1
        end
        copyto!(X, X .- real(dot(p, X)) .* p)
        X .*= œÄ / norm(X)
    else
        Œ∏ = acos(cosŒ∏)
        X .= (q .- cosŒ∏ .* p) ./ usinc(Œ∏)
    end
    return project!(M, X, p, X)
end

@doc raw"""
    manifold_dimension(M::AbstractSphere)

Return the dimension of the [`AbstractSphere`](@ref) `M`, respectively i.e. the
dimension of the embedding -1.
"""
manifold_dimension(M::AbstractSphere) = manifold_dimension(get_embedding(M)) - 1

"""
    mean(
        S::AbstractSphere,
        x::AbstractVector,
        [w::AbstractWeights,]
        method = GeodesicInterpolationWithinRadius(œÄ/2);
        kwargs...,
    )

Compute the Riemannian [`mean`](@ref mean(M::Manifold, args...)) of `x` using
[`GeodesicInterpolationWithinRadius`](@ref).
"""
mean(::AbstractSphere, ::Any...)

function Statistics.mean!(
    S::AbstractSphere,
    p,
    x::AbstractVector,
    w::AbstractVector;
    kwargs...,
)
    return mean!(S, p, x, w, GeodesicInterpolationWithinRadius(œÄ / 2); kwargs...)
end

function mid_point!(S::Sphere, q, p1, p2)
    cosŒ∏ = real(dot(p1, p2))
    q .= p1 .+ p2
    project!(S, q, q)
    return q
end

"""
    normal_tvector_distribution(S::Sphere{n,‚Ñù}, p, œÉ)

Generate a distribution in the tangent space at `p` by generating a
normal distribution in ambient space with standard deviation `œÉ`
projected to the tangent space at `p`.
"""
function normal_tvector_distribution(S::Sphere{n,‚Ñù}, p, œÉ) where {n}
    d = Distributions.MvNormal(zero(p), œÉ)
    return ProjectedFVectorDistribution(TangentBundleFibers(S), p, d, project!, p)
end

@doc raw"""
    project(M::AbstractSphere, p)

Project the point `p` from the embedding onto the [`Sphere`](@ref) `M`.

````math
    \operatorname{proj}(p) = \frac{p}{\lVert p \rVert},
````
where $\lVert\cdot\rVert$ denotes the usual 2-norm for vectors if $m=1$ and the Frobenius
norm for the case $m>1$.
"""
project(::AbstractSphere, ::Any)

project!(::AbstractSphere, q, p) = (q .= p ./ norm(p))

@doc raw"""
    project(M::AbstractSphere, p, X)

Project the point `X` onto the tangent space at `p` on the [`Sphere`](@ref) `M`.

````math
\operatorname{proj}_{p}(X) = X - ‚ü®p, X‚ü©p
````
"""
project(::AbstractSphere, ::Any, ::Any)

project!(S::AbstractSphere, Y, p, X) = (Y .= X .- real(dot(p, X)) .* p)

@doc raw"""
    representation_size(M::AbstractSphere)

Return the size points on the [`AbstractSphere`](@ref) `M` are represented as, i.e., the
representation size of the embedding.
"""
@generated representation_size(M::ArraySphere{N}) where {N} = size_to_tuple(N)
@generated representation_size(M::Sphere{N}) where {N} = (N + 1,)


@doc raw"""
    retract(M::AbstractSphere, p, X, ::ProjectionRetraction)

Compute the retraction that is based on projection, i.e.

````math
\operatorname{retr}_p(X) = \frac{p+X}{\lVert p+X \rVert_2}
````
"""
retract(::AbstractSphere, ::Any, ::Any, ::ProjectionRetraction)

function retract!(M::AbstractSphere, q, p, X, ::ProjectionRetraction)
    q .= p .+ X
    return project!(M, q, q)
end

Base.show(io::IO, ::Sphere{n,ùîΩ}) where {n,ùîΩ} = print(io, "Sphere($(n), $(ùîΩ))")
function Base.show(io::IO, ::ArraySphere{N,ùîΩ}) where {N,ùîΩ}
    return print(io, "ArraySphere($(join(N.parameters, ", ")); field = $(ùîΩ))")
end

"""
    uniform_distribution(M::Sphere{n,‚Ñù}, p) where {n}

Uniform distribution on given [`Sphere`](@ref) `M`. Generated points will be of
similar type as `p`.
"""
function uniform_distribution(M::Sphere{n,‚Ñù}, p) where {n}
    d = Distributions.MvNormal(zero(p), 1.0)
    return ProjectedPointDistribution(M, d, project!, p)
end

@doc raw"""
    vector_transport_to(M::AbstractSphere, p, X, q, ::ParallelTransport)

Compute the parallel transport on the [`Sphere`](@ref) of the tangent vector `X` at `p`
to `q`, provided, the [`geodesic`](@ref) between `p` and `q` is unique. The formula reads

````math
P_{p‚Üêq}(X) = X - \frac{\langle \log_p q,X\rangle_p}{d^2_ùïä(p,q)}
\bigl(\log_p q + \log_q p \bigr).
````
"""
vector_transport_to(::AbstractSphere, ::Any, ::Any, ::Any, ::Any, ::ParallelTransport)

function vector_transport_to!(::AbstractSphere, Y, p, X, q, ::ParallelTransport)
    m = p .+ q
    mnorm2 = real(dot(m, m))
    factor = 2 * real(dot(X, q)) / mnorm2
    Y .= X .- m .* factor
    return Y
end
