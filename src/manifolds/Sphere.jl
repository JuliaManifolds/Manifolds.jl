@doc raw"""
    Sphere{N,M,ğ”½} <: AbstractEmbeddedManifold{DefaultEmbeddingType}

The (unit) sphere manifold $ğ•Š^{m,n}$ is the set of all unit norm elements. The sphere is
represented in the embedding, and currently supports both vectors and matrices, i.e.

````math
ğ•Š^{n,m} := \bigl\{ p \in ğ”½^{m,n}\ \big|\ \lVert p \rVert = 1 \bigr\}
````

where $ğ”½\in\{â„,â„‚\}. For $m=1$ and $ğ”½=â„$ this  simplifies to unit vectors in $â„^n$ and the
more common notation is $ğ•Š^{n,m} â‰œ ğ•Š^{n-1}$.

The tangent space at point p is given by

````math
T_pğ•Š^{n,m} := \bigl\{ X âˆˆ ğ”½^{n,m} : âŸ¨p,XâŸ© = 0 \bigr \},
````

where $ğ”½\in\{â„,â„‚\} and $âŸ¨\cdot,\cdotâŸ©$ denotes the inner product in the
embedding $\mathbb ğ”½^{n+1}$.

This manifold is modeled as an embedded manifold to the [`Euclidean`](@ref), i.e.
several functions like the [`inner`](@ref) product and the [`zero_tangent_vector`](@ref)
are inherited from the embedding.

# Constructor

    Sphere(n[, field=â„])

Generate $ğ•Š^{n+1,1} âŠ‚ â„^{n+1}$.

    Sphere(n,m[, field=â„])

Generate $ğ•Š^{n,m} âŠ‚ â„^{n,m}$.
"""
struct Sphere{n,m,ğ”½} <: AbstractEmbeddedManifold{DefaultIsometricEmbeddingType} end

Sphere(n::Int, field::AbstractNumbers=â„) = Sphere{n+1,1,field}()
Sphere(n::Int, m::Int, field::AbstractNumbers=â„) = Sphere{n,m,field}()

"""
    check_manifold_point(M, p; kwargs...)

Check whether `p` is a valid point on the [`Sphere`](@ref) `M`, i.e. is a vector
of length [`manifold_dimension`](@ref)`(M)+1` (approximately) of unit length.
The tolerance for the last test can be set using the `kwargs...`.
"""
function check_manifold_point(M::Sphere, p; kwargs...)
    mpv = invoke(
        check_manifold_point,
        Tuple{(typeof(get_embedding(M))), typeof(p)},
        get_embedding(M),
        p;
        kwargs...
    )
    mpv === nothing || return mpv
    if !isapprox(norm(p), 1.0; kwargs...)
        return DomainError(
            norm(p),
            "The point $(p) does not lie on the sphere $(M) since its norm is not 1.",
        )
    end
    return nothing
end

"""
    check_tangent_vector(M, p, X; check_base_point = true, kwargs... )

Check whether `X` is a tangent vector to `p` on the [`Sphere`](@ref) `M`, i.e.
after [`check_manifold_point`](@ref)`(M,p)`, `X` has to be of same dimension as `p`
and orthogonal to `p`.
The optional parameter `check_base_point` indicates, whether to call [`check_manifold_point`](@ref)  for `p`.
The tolerance for the last test can be set using the `kwargs...`.
"""
function check_tangent_vector(
    M::Sphere,
    p,
    X;
    check_base_point = true,
    kwargs...,
)
    if check_base_point
        mpe = check_manifold_point(M, p; kwargs...)
        mpe === nothing || return mpe
    end
    mpv = invoke(
        check_tangent_vector,
        Tuple{typeof(get_embedding(M)), typeof(p), typeof(X)},
        get_embedding(M),
        p,
        X;
        check_base_point = false, # already checked above
        kwargs...
    )
    mpv === nothing || return mpv
    if !isapprox(abs(real(dot(p, X))), 0.0; kwargs...)
        return DomainError(
            abs(dot(p, X)),
            "The vector $(X) is not a tangent vector to $(p) on $(M), since it is not orthogonal in the embedding.",
        )
    end
    return nothing
end

decorated_manifold(M::Sphere{n,m,ğ”½}) where {n,m,ğ”½}= Euclidean(n,m; field=ğ”½)
decorated_manifold(M::Sphere{n,1,ğ”½}) where {n,ğ”½}= Euclidean(n; field=ğ”½)

@doc raw"""
    distance(M::Sphere, p, q)

Compute the geodesic distance betweeen `p` and `q` on the [`Sphere`](@ref) `M`.
The formula is given by the (shorter) great arc length on the (or a) great circle
both `p` and `q` lie on.

````math
d_{ğ•Š^n}(p,q) = \arccos(âŸ¨p,qâŸ©).
````
"""
distance(::Sphere, p, q) = acos(clamp(real(dot(p, q)), -1, 1))

embed!(::Sphere, q, p) = (q .= p)

embed!(::Sphere, Y, p, X) = (Y .= X)

@doc raw"""
    exp(M::Sphere, p, X)

Compute the exponential map from `p` in the tangent direction `X` on the [`Sphere`](@ref)
`M` by following the great arc eminating from `p` in direction `X`.

````math
\exp_p X = \cos(\lVert X \rVert_p)p + \sin(\lVert X \rVert_p)\frac{X}{\lVert X \rVert_p}X,
````
where $\lVert X \rVert_p$ is the [`norm`](@ref norm(::Sphere,p,X)) on the
[`Sphere`](@ref) `M`.
"""
exp(::Sphere, ::Any...)

function exp!(M::Sphere, q, p, X)
    Î¸ = norm(M, p, X)
    q .= cos(Î¸) .* p .+ usinc(Î¸) .* X
    return q
end

flat!(M::Sphere, Î¾::CoTFVector, p, X::TFVector) = copyto!(Î¾, X)

function get_basis(M::Sphere{n,1,â„}, p, B::DiagonalizingOrthonormalBasis{T,â„}) where {T,n}
    A = zeros(n, n)
    A[1, :] = transpose(p)
    A[2, :] = transpose(B.frame_direction)
    V = nullspace(A)
    Îº = ones(n)
    if !iszero(B.frame_direction)
        # if we have a nonzero direction for the geodesic, add it and it gets curvature zero from the tensor
        V = cat(B.frame_direction / norm(M, p, B.frame_direction), V; dims = 2)
        Îº[1] = 0 # no curvature along the geodesic direction, if x!=y
    end
    Î = [V[:, i] for i = 1:manifold_dimension(M)]
    return CachedBasis(B, Îº, Î)
end

@doc raw"""
    get_coordinates(M::Sphere, p, X, B::DefaultOrthonormalBasis)

Represent the tangent vector `X` at point `p` from the [`Sphere`](@ref) `M` in
an orthonormal basis by rotating the vector `X` using the rotation matrix
$2\frac{q q^\mathrm{T}}{q^\mathrm{T} q} - I$ where $q = p + (1, 0, â€¦, 0)$.
"""
function get_coordinates(M::Sphere{n,1,â„}, p, X, B::DefaultOrthonormalBasis) where {n}
    if isapprox(abs(p[1]), 1)
        return X[2:end]
    else
        xp1 = p .+ ntuple(i -> ifelse(i == 1, 1, 0), n + 1)
        return (2*xp1*real(dot(xp1, X))/real(dot(xp1, xp1))-X)[2:end]
    end
end

function get_coordinates!(M::Sphere, Y, p, X, B::DefaultOrthonormalBasis)
    return copyto!(Y, get_coordinates(M, p, X, B))
end

function get_vector(M::Sphere{n,1,â„}, p, X, B::DefaultOrthonormalBasis) where {n}
    p[1] â‰ˆ 1 && return vcat(0, X)
    xp1 = p .+ ntuple(i -> ifelse(i == 1, 1, 0), n + 1)
    X0 = vcat(0, X)
    return 2 * xp1 * real(dot(xp1, X0)) / real(dot(xp1, xp1)) - X0
end

function get_vector!(M::Sphere, Y::AbstractVector, p, X, B::DefaultOrthonormalBasis)
    return copyto!(Y, get_vector(M, p, X, B))
end

@doc raw"""
    injectivity_radius(M::Sphere[, p])

Return the injectivity radius for the [`Sphere`](@ref) `M`, which is globally $Ï€$.

    injectivity_radius(M::Sphere, x, ::ProjectionRetraction)

Return the injectivity radius for the [`ProjectionRetraction`](@ref) on the
[`Sphere`](@ref), which is globally $\frac{Ï€}{2}$.
"""
injectivity_radius(::Sphere) = Ï€
injectivity_radius(::Sphere, ::ExponentialRetraction) = Ï€
injectivity_radius(::Sphere, ::ProjectionRetraction) = Ï€ / 2
injectivity_radius(::Sphere, ::Any) = Ï€
injectivity_radius(::Sphere, ::Any, ::ExponentialRetraction) = Ï€
injectivity_radius(::Sphere, ::Any, ::ProjectionRetraction) = Ï€ / 2
eval(quote
    @invoke_maker 1 Manifold injectivity_radius(M::Sphere, rm::AbstractRetractionMethod)
end)

@doc raw"""
    inverse_retract(M::Sphere, p, q, ::ProjectionInverseRetraction)

Compute the inverse of the projection based retraction on the [`Sphere`](@ref),
i.e. rearranging $p+X = q\lVert p+X\rVert_2$ yields
since $âŸ¨p,XâŸ© = 0$ and when $d_{ğ•Š^2}(p,q) â‰¤ \frac{Ï€}{2}$ that

````math
\operatorname{retr}_p^{-1}(q) = \frac{q}{âŸ¨p, qâŸ©} - p.
````
"""
inverse_retract(::Sphere, ::Any, ::Any, ::ProjectionInverseRetraction)

function inverse_retract!(::Sphere, X, p, q, ::ProjectionInverseRetraction)
    return (X .= q ./ real(dot(p, q)) .- p)
end

@doc raw"""
    log(M::Sphere, p, q)

Compute the logarithmic map on the [`Sphere`](@ref) `M`, i.e. the tangent vector,
whose geodesic starting from `p` reaches `q` after time 1.
The formula reads for $x â‰  -y$

````math
\log_p q = d_{ğ•Š^n}(p,q) \frac{q-âŸ¨p,qâŸ© p}{\lVert q-âŸ¨p,qâŸ© p \rVert_2},
````

and a deterministic choice from the set of tangent vectors is returned if $x=-y$, i.e. for
opposite points.
"""
log(::Sphere, ::Any...)

function log!(S::Sphere, X, p, q)
    cosÎ¸ = real(dot(p, q))
    if cosÎ¸ â‰ˆ -1 # appr. opposing points, return deterministic choice from set-valued log
        fill!(X, 0)
        if p[1] â‰ˆ 1
            X[2] = 1
        else
            X[1] = 1
        end
        copyto!(X, X .- real(dot(p, X)) .* p)
        X .*= Ï€ / norm(X)
    else
        cosÎ¸ = cosÎ¸ > 1 ? one(cosÎ¸) : cosÎ¸
        Î¸ = acos(cosÎ¸)
        X .= (q .- cosÎ¸ .* p) ./ usinc(Î¸)
    end
    return project!(S, X, p, X)
end

@doc raw"""
    manifold_dimension(M::Sphere)

Return the dimension of the [`Sphere`](@ref)`(n) `M`, i.e. $ğ•Š^n$, which is $\dim(ğ•Š^n) = n$.
"""
manifold_dimension(S::Sphere{n,m,â„}) where {n,m} = n*m-1
manifold_dimension(S::Sphere{n,m,â„‚}) where {n,m} = 2*n*m-1

"""
    mean(
        S::Sphere,
        x::AbstractVector,
        [w::AbstractWeights,]
        method = GeodesicInterpolationWithinRadius(Ï€/2);
        kwargs...,
    )

Compute the Riemannian [`mean`](@ref mean(M::Manifold, args...)) of `x` using
[`GeodesicInterpolationWithinRadius`](@ref).
"""
mean(::Sphere, ::Any...)

function mean!(S::Sphere, p, x::AbstractVector, w::AbstractVector; kwargs...)
    return mean!(S, p, x, w, GeodesicInterpolationWithinRadius(Ï€ / 2); kwargs...)
end

"""
    normal_tvector_distribution(S::Sphere, p, Ïƒ)

Normal distribution in ambient space with standard deviation `Ïƒ`
projected to tangent space at `p`.
"""
function normal_tvector_distribution(S::Sphere, p, Ïƒ)
    d = Distributions.MvNormal(zero(p), Ïƒ)
    return ProjectedFVectorDistribution(TangentBundleFibers(S), p, d, project_vector!, p)
end

@doc raw"""
    project(M::Sphere, p)

Project the point `p` from the embedding onto the [`Sphere`](@ref) `M`.

````math
    \operatorname{proj}_{ğ•Š^{n,m}}(p) = \frac{p}{\lVert p \rVert},
````
where $\lVert\cdot\rVert$ denotes the usual 2-norm for vectors if $m=1$ and the Frobenius
norm for the case $m>1$.
"""
project(::Sphere, ::Any)

project!(S::Sphere, q, p) = copyto!(q, p./ norm(p))

@doc raw"""
    project(M::Sphere, p, X)

Project the point `X` onto the tangent space at `p` on the [`Sphere`](@ref) `M`.

````math
\operatorname{proj}_{p}(X) = X - âŸ¨p, XâŸ©p
````
"""
project(::Sphere, ::Any, ::Any)

project!(S::Sphere, Y, p, X) = (Y .= X .- real(dot(p, X)) .* p)

@doc raw"""
    representation_size(M::Sphere)

Return the size points on the [`Sphere`](@ref) `M` are represented as, i.e.
for the `n`-dimensional [`Sphere`](@ref) it is vectors of size `(n+1,)` and
for (Forbenius-)unit-norm matrixes (n,m).
"""
@generated representation_size(::Sphere{n,1}) where {n} = (n,)
@generated representation_size(::Sphere{n,m}) where {n,m} = (n,m,)

@doc raw"""
    retract(M::Sphere, p, X, ::ProjectionRetraction)

Compute the retraction that is based on projection, i.e.

````math
\operatorname{retr}_p(X) = \frac{p+X}{\lVert p+X \rVert_2}
````
"""
retract(::Sphere, ::Any, ::Any, ::ProjectionRetraction)

function retract!(M::Sphere, q, p, X, ::ProjectionRetraction)
    q .= p .+ X
    return project!(M, q, q)
end

show(io::IO, ::Sphere{n,1,â„}) where {n} = print(io, "Sphere($(n-1))")
show(io::IO, ::Sphere{n,m,â„}) where {n,m} = print(io, "Sphere($(n),$(m))")
show(io::IO, ::Sphere{n,1,â„‚}) where {n} = print(io, "Sphere($(n-1); field = â„‚)")
show(io::IO, ::Sphere{n,m,â„‚}) where {n,m} = print(io, "Sphere($(n),$(m); field = â„‚)")

"""
    uniform_distribution(M::Sphere, p)

Uniform distribution on given [`Sphere`](@ref) `M`. Generated points will be of
similar type as `p`.
"""
function uniform_distribution(M::Sphere, p)
    d = Distributions.MvNormal(zero(p), 1.0)
    return ProjectedPointDistribution(M, d, project!, p)
end

@doc doc"""
    vector_transport_to(M::Sphere, p, X, q, ::ParallelTransport)

Compute the paralllel transport on the [`Sphere`](@ref) of the tangent vector `X` at `p`
to `q`, provided, the [`geodesic`](@ref) between `p` and `q` is unique. The formula reads

````math
P_{pâ†q}(X) = X - \frac{\langle \log_p q,X\rangle_p}{d^2_{ğ•Š^{n,m}}(p,q)}
\bigl(\log_xy + \log_yx \bigr).
````
"""
vector_transport_to(::Sphere, ::Any, ::Any, ::Any, ::Any, ::ParallelTransport)

function vector_transport_to!(M::Sphere, Y, p, X, q, ::ParallelTransport)
    X_pq = log(M, p, q)
    Xl = norm(M, p, X_pq)
    copyto!(Y, X)
    if Xl > 0
        factor = 2 * real(dot(X, q)) / (norm(p + q)^2)
        Y .-= factor .* (p .+ q)
    end
    return Y
end
