
@doc raw"""
    const UnitaryMatrices{n,ğ”½} = AbstarctUnitaryMatrices{n,ğ”½,AbsoluteDeterminantOneMatrices}

The manifold ``U(n,ğ”½)`` of ``nÃ—n`` complex matrices (when ğ”½=â„‚) or quaternionic matrices
(when ğ”½=â„) such that

``p^{\mathrm{H}}p = \mathrm{I}_n,``

where ``\mathrm{I}_n`` is the ``nÃ—n`` identity matrix.
Such matrices `p` have a property that ``\lVert \det(p) \rVert = 1``.

The tangent spaces are given by

```math
    T_pU(n) \coloneqq \bigl\{
    X \big| pY \text{ where } Y \text{ is skew symmetric, i. e. } Y = -Y^{\mathrm{H}}
    \bigr\}
```

But note that tangent vectors are represented in the Lie algebra, i.e. just using ``Y`` in
the representation above.
If you prefer the representation as `X` you can use the [`Stiefel`](@ref)`(n, n, â„‚)` manifold.

# Constructor

    UnitaryMatrices(n, ğ”½::AbstractNumbers=â„‚)

see also [`OrthogonalMatrices`](@ref) for the real valued case.
"""
const UnitaryMatrices{T,ğ”½} = GeneralUnitaryMatrices{T,ğ”½,AbsoluteDeterminantOneMatrices}

function UnitaryMatrices(n::Int, ğ”½::AbstractNumbers=â„‚; parameter::Symbol=:type)
    size = wrap_type_parameter(parameter, (n,))
    return UnitaryMatrices{typeof(size),ğ”½}(size)
end

check_size(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p::Number) = nothing
check_size(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X::Number) = nothing

embed(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p::Number) = SMatrix{1,1}(p)

embed(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X::Number) = SMatrix{1,1}(X)

function exp(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X::Number)
    return p * exp(X)
end
function exp(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X::Number, t::Number)
    return p * exp(t * X)
end

function get_coordinates_orthonormal(
    ::UnitaryMatrices{TypeParameter{Tuple{1}},â„},
    p,
    X::Quaternions.Quaternion,
    ::QuaternionNumbers,
)
    return @SVector [X.v1, X.v2, X.v3]
end

function get_vector_orthonormal(
    ::UnitaryMatrices{TypeParameter{Tuple{1}},â„},
    p::Quaternions.Quaternion,
    c,
    ::QuaternionNumbers,
)
    i = firstindex(c)
    return Quaternions.quat(0, c[i], c[i + 1], c[i + 2])
end

injectivity_radius(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}) = Ï€

function _isapprox(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, x, y; kwargs...)
    return isapprox(x[], y[]; kwargs...)
end
function _isapprox(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X, Y; kwargs...)
    return isapprox(X[], Y[]; kwargs...)
end

function log(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p::Number, q::Number)
    return log(conj(p) * q)
end

@doc raw"""
    manifold_dimension(M::UnitaryMatrices{n,â„‚}) where {n}

Return the dimension of the manifold unitary matrices.
```math
\dim_{\mathrm{U}(n)} = n^2.
```
"""
function manifold_dimension(M::UnitaryMatrices{<:Any,â„‚})
    n = get_parameter(M.size)[1]
    return n^2
end
@doc raw"""
    manifold_dimension(M::UnitaryMatrices{<:Any,â„})

Return the dimension of the manifold unitary matrices.
```math
\dim_{\mathrm{U}(n, â„)} = n(2n+1).
```
"""
function manifold_dimension(M::UnitaryMatrices{<:Any,â„})
    n = get_parameter(M.size)[1]
    return n * (2n + 1)
end

number_of_coordinates(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, ::AbstractBasis{â„}) = 3

project(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p) = sign(p)

project(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p, X) = (X - conj(X)) / 2

@doc raw"""
    project(M::UnitaryMatrices, p, X)

Project `X` onto the tangent space of `p` to the [`UnitaryMatrices`](@ref) manifold `M`.
The formula reads

````math
\operatorname{proj}_{T_p\mathcal M}(X) = X - p \operatorname{Sym}(p^{\mathrm{H}}X),
````

where ``\operatorname{Sym}(q)`` is the symmetrization of ``q``, e.g. by
``\operatorname{Sym}(q) = \frac{q^{\mathrm{H}}+q}{2}``.
"""
project(::UnitaryMatrices, ::Any...)

function Random.rand(M::UnitaryMatrices{TypeParameter{Tuple{1}},â„}; vector_at=nothing)
    if vector_at === nothing
        return sign(rand(Quaternions.QuaternionF64))
    else
        project(M, vector_at, rand(Quaternions.QuaternionF64))
    end
end
function Random.rand(
    rng::AbstractRNG,
    M::UnitaryMatrices{TypeParameter{Tuple{1}},â„};
    vector_at=nothing,
)
    if vector_at === nothing
        return sign(rand(rng, Quaternions.QuaternionF64))
    else
        project(M, vector_at, rand(rng, Quaternions.QuaternionF64))
    end
end

@doc raw"""
    rand(::Unitary; vector_at=nothing, Ïƒ::Real=1.0)

Gereate a random point on the [`UnitaryMatrices`](@ref) manifold,
if `vector_at` is nothing, by computing the QR decomposition of
a ``nÃ—x`` matrix.

Generate a tangent vector at `vector_at` by projecting a normally
distributed matrix onto the tangent space.
"""
rand(::UnitaryMatrices; Ïƒ::Real=1.0)

function Random.rand!(
    rng::AbstractRNG,
    M::UnitaryMatrices,
    pX;
    vector_at=nothing,
    Ïƒ::Real=one(real(eltype(pX))),
)
    n = get_parameter(M.size)[1]
    if vector_at === nothing
        A = Ïƒ * randn(rng, eltype(pX), n, n)
        pX .= Matrix(qr(A).Q)
    else
        Z = Ïƒ * randn(rng, eltype(pX), size(pX))
        project!(M, pX, vector_at, Z)
    end
    return pX
end

function Base.show(io::IO, ::UnitaryMatrices{TypeParameter{Tuple{n}},â„‚}) where {n}
    return print(io, "UnitaryMatrices($(n))")
end
function Base.show(io::IO, M::UnitaryMatrices{Tuple{Int},â„‚})
    n = get_parameter(M.size)[1]
    return print(io, "UnitaryMatrices($n; parameter=:field)")
end
function Base.show(io::IO, ::UnitaryMatrices{TypeParameter{Tuple{n}},â„}) where {n}
    return print(io, "UnitaryMatrices($(n), â„)")
end
function Base.show(io::IO, M::UnitaryMatrices{Tuple{Int},â„})
    n = get_parameter(M.size)[1]
    return print(io, "UnitaryMatrices($n, â„; parameter=:field)")
end

@doc raw"""
    riemannian_Hessian(M::UnitaryMatrices, p, G, H, X)

The Riemannian Hessian can be computed by adopting Eq. (5.6) [Nguyen:2023](@cite),
so very similar to the complex Stiefel manifold.
The only difference is, that here the tangent vectors are stored
in the Lie algebra, i.e. the update direction is actually ``pX`` instead of just ``X`` (in Stiefel).
and that means the inverse has to be appliead to the (Euclidean) Hessian
to map it into the Lie algebra.
"""
riemannian_Hessian(M::UnitaryMatrices, p, G, H, X)
function riemannian_Hessian!(M::UnitaryMatrices, Y, p, G, H, X)
    symmetrize!(Y, G' * p)
    project!(M, Y, p, p' * H - X * Y)
    return Y
end

@doc raw"""
    Weingarten(M::UnitaryMatrices, p, X, V)

Compute the Weingarten map ``\mathcal W_p`` at `p` on the [`Stiefel`](@ref) `M` with respect to the
tangent vector ``X \in T_p\mathcal M`` and the normal vector ``V \in N_p\mathcal M``.

The formula is due to [AbsilMahonyTrumpf:2013](@cite) given by

```math
\mathcal W_p(X,V) = -\frac{1}{2}p\bigl(V^{\mathrm{H}}X - X^\mathrm{H}V\bigr)
```
"""
Weingarten(::UnitaryMatrices, p, X, V)

function Weingarten!(::UnitaryMatrices, Y, p, X, V)
    Y .= V' * X
    Y .= -p * 1 / 2 * (Y - Y')
    return Y
end

zero_vector(::UnitaryMatrices{TypeParameter{Tuple{1}},â„}, p) = zero(p)
