@doc raw"""
    Circle{ğ”½} <: Manifold

The circle $ğ•Š^1$ is a manifold here represented by
real-valued points in $[-Ï€,Ï€)$ or complex-valued points $z âˆˆ â„‚$ of absolute value
$\lvert z\rvert = 1$.
# Constructor

    Circle(â„)

Generate the `â„`-valued Circle represented by angles, which
alternatively can be set to use the [`AbstractNumbers`](@ref) `ğ”½=â„‚` to obtain the `Circle`
represented by `â„‚`-valued `Circle` of unit numbers.
"""
struct Circle{ğ”½} <: Manifold where {ğ”½<:AbstractNumbers} end

Circle(field::AbstractNumbers = â„) = Circle{field}()

@doc raw"""
    check_manifold_point(M::Circle, p)

Check whether `p` is a point on the [`Circle`](@ref) `M`.
For the real-valued case, `x` is an angle and hence it checks that $p  âˆˆ [-Ï€,Ï€)$.
for the complex-valued case, it is a unit number, $p âˆˆ â„‚$ with $\lvert p \rvert = 1$.
"""
check_manifold_point(::Circle, ::Any...)

function check_manifold_point(M::Circle{â„}, p; kwargs...)
    if !isapprox(sym_rem(p), p; kwargs...)
        return DomainError(
            p,
            "The point $(p) does not lie on $(M), since its is not in [-Ï€,Ï€).",
        )
    end
    return nothing
end
function check_manifold_point(M::Circle{â„‚}, p; kwargs...)
    if !isapprox(sum(abs.(p)), 1.0; kwargs...)
        return DomainError(
            abs(p),
            "The point $(p) does not lie on the $(M) since its norm is not 1.",
        )
    end
    return nothing
end

"""
    check_tangent_vector(M::Circle, p, X; check_base_point, kwargs...)

Check whether `X` is a tangent vector in the tangent space of `p` on the
[`Circle`](@ref) `M`.
For the real-valued case represented by angles, all `X` are valid, since the tangent space is the whole real line.
For the complex-valued case `X` has to lie on the line parallel to the tangent line at `p`
in the complex plane, i.e. their inner product has to be zero.
The optional parameter `check_base_point` indicates, whether to call [`check_manifold_point`](@ref)  for `p`.
"""
check_tangent_vector(::Circle{â„}, ::Any...; ::Any...)

function check_tangent_vector(M::Circle{â„}, p, X; check_base_point = true, kwargs...)
    if check_base_point
        perr = check_manifold_point(M, p; kwargs...)
        return perr # if x is valid all v that are real numbers are valid
    else
        return nothing
    end
end
function check_tangent_vector(M::Circle{â„‚}, p, X; check_base_point = true, kwargs...)
    if check_base_point
        perr = check_manifold_point(M, p)
        perr === nothing || return perr
    end
    if !isapprox(abs(complex_dot(p, X)), 0.0; kwargs...)
        return DomainError(
            abs(complex_dot(p, X)),
            "The value $(X) is not a tangent vector to $(p) on $(M), since it is not orthogonal in the embedding.",
        )
    end
    return nothing
end

@doc raw"""
    complex_dot(a, b)

Compute the inner product of two (complex) numbers with in the complex plane.
"""
complex_dot(a, b) = dot(map(real, a), map(real, b)) + dot(map(imag, a), map(imag, b))
complex_dot(a::Number, b::Number) = (real(a) * real(b) + imag(a) * imag(b))

@doc raw"""
    distance(M::Circle, p, q)

Compute the distance on the [`Circle`](@ref) `M`, which is
the absolute value of the symmetric remainder of `p` and `q` for the real-valued
case and the angle between both complex numbers in the Gaussian plane for the
complex-valued case.
"""
distance(::Circle, ::Any...)
distance(::Circle{â„}, p::Real, q::Real) = abs(sym_rem(p - q))
distance(::Circle{â„}, p, q) = abs(sum(sym_rem.(p - q)))
distance(::Circle{â„‚}, p, q) = acos(clamp(complex_dot(p, q), -1, 1))

@doc raw"""
    exp(M::Circle, p, X)

Compute the exponential map on the [`Circle`](@ref).
````math
\exp_p X = (p+X)_{2Ï€},
````
where $(\cdot)_{2Ï€}$ is the (symmetric) remainder with respect to division by $2Ï€$, i.e. in $[-Ï€,Ï€)$.

For the complex-valued case, the same formula as for the [`Sphere`](@ref) $ğ•Š^1$ is applied to values in the
complex plane.
"""
exp(::Circle, ::Any...)
exp(::Circle{â„}, p::Real, X::Real) = sym_rem(p + X)
function exp(M::Circle{â„‚}, x::Number, v::Number)
    Î¸ = norm(M, x, v)
    return cos(Î¸) * x + usinc(Î¸) * v
end

exp!(::Circle{â„}, q, p, X) = (q .= sym_rem(p + X))
function exp!(M::Circle{â„‚}, q, p, X)
    Î¸ = norm(M, p, X)
    q .= cos(Î¸) * p + usinc(Î¸) * X
    return q
end

flat(M::Circle, p::Number, X::TFVector) = FVector(CotangentSpace, X.data)

flat!(::Circle, Î¾::CoTFVector, p, X::TFVector) = copyto!(Î¾, X)

function get_basis(M::Circle{â„}, p, B::DiagonalizingOrthonormalBasis)
    sbv = sign(B.frame_direction[])
    vs = @SVector [@SVector [sbv == 0 ? one(sbv) : sbv]]
    return CachedBasis(B, (@SVector [0]) , vs)
end

get_coordinates(M::Circle{â„}, p, X, B::DefaultOrthonormalBasis) = X
function get_coordinates(M::Circle{â„}, p, X, B::DiagonalizingOrthonormalBasis)
    sbv = sign(B.frame_direction[])
    return X .* (sbv == 0 ? 1 : sbv)
end
"""
    get_coordinates(M::Circle{â„‚}, p, X, B::DefaultOrthonormalBasis)

Return tangent vector coordinates in the Lie algebra of the circle.
"""
function get_coordinates(M::Circle{â„‚}, p, X, B::DefaultOrthonormalBasis)
    X, p = X[1], p[1]
    Xâ± = imag(X) * real(p) - real(X) * imag(p)
    return @SVector [Xâ±]
end

function get_coordinates!(M::Circle, Y::AbstractArray, p, X, B::DefaultOrthonormalBasis)
    Y[] = get_coordinates(M, p, X, B)[]
    return Y
end
function get_coordinates!(
    M::Circle,
    Y::AbstractArray,
    p,
    X,
    B::DiagonalizingOrthonormalBasis,
)
    Y[] = get_coordinates(M, p, X, B)[]
    return Y
end
function get_coordinates!(
    M::Circle,
    Y::AbstractArray,
    p,
    X,
    B::VeeOrthogonalBasis,
)
    return invoke(
        get_coordinates!,
        Tuple{Manifold,Any,Any,Any,VeeOrthogonalBasis},
        M,
        Y,
        p,
        X,
        B,
    )
end

get_vector(M::Circle{â„}, p, X, B::AbstractBasis) = X
get_vector(M::Circle{â„}, p, X, B::DefaultOrthonormalBasis) = X
function get_vector(M::Circle{â„}, p, X, B::DiagonalizingOrthonormalBasis)
    sbv = sign(B.frame_direction[])
    return X .* (sbv == 0 ? 1 : sbv)
end
"""
    get_vector(M::Circle{â„‚}, p, X, B::DefaultOrthonormalBasis)

Return tangent vector from the coordinates in the Lie algebra of the circle.
"""
get_vector(M::Circle{â„‚}, p, X, B::AbstractBasis) = @SVector [1im * X[1] * p[1]]

function get_vector!(M::Circle, Y::AbstractArray, p, X, B::AbstractBasis)
    Y[] = get_vector(M, p, X, B)[]
    return Y
end
@superinvoke_maker 5 get_vector!(M::Circle, Y::AbstractArray, p, X, B::DefaultOrthonormalBasis)
@superinvoke_maker 5 get_vector!(M::Circle, Y::AbstractArray, p, X, B::DiagonalizingOrthonormalBasis)
@superinvoke_maker 5 get_vector!(M::Circle, Y::AbstractArray, p, X, B::DefaultBasis)
@superinvoke_maker 5 get_vector!(M::Circle, Y::AbstractArray, p, X, B::DefaultOrthogonalBasis)
@superinvoke_maker 5 get_vector!(M::Circle, Y::AbstractArray, p, X, B::VeeOrthogonalBasis)

@doc raw"""
    injectivity_radius(M::Circle[, p])

Return the injectivity radius on the [`Circle`](@ref) `M`, i.e. $Ï€$.
"""
injectivity_radius(::Circle, args...) = Ï€

@doc raw"""
    inner(M::Circle, p, X, Y)

Compute the inner product of the two tangent vectors `X,Y` from the tangent plane at `p` on
the [`Circle`](@ref) `M` using the restriction of the metric from the embedding,
i.e.

````math
g_p(X,Y) = X*Y
````

for the real case and

````math
g_p(X,Y) = Y^\mathrm{T}X
````

for the complex case interpreting complex numbers in the Gaussian plane.
"""
inner(::Circle, ::Any...)
@inline inner(::Circle{â„}, p, X, Y) = dot(X, Y)
@inline inner(::Circle{â„}, p::Real, X::Real, Y::Real) = X * Y
@inline inner(::Circle{â„‚}, p, X, Y) = complex_dot(X, Y)

function inverse_retract(M::Circle, x::Number, y::Number)
    return inverse_retract(M, x, y, LogarithmicInverseRetraction())
end
function inverse_retract(M::Circle, x::Number, y::Number, ::LogarithmicInverseRetraction)
    return log(M, x, y)
end

@doc raw"""
    log(M::Circle, p, q)

Compute the logarithmic map on the [`Circle`](@ref) `M`.
````math
\log_p q = (q-p)_{2Ï€},
````
where $(\cdot)_{2Ï€}$ is the (symmetric) remainder with respect to division by $2Ï€$, i.e. in $[-Ï€,Ï€)$.

For the complex-valued case, the same formula as for the [`Sphere`](@ref) $ğ•Š^1$ is applied to values in the
complex plane.
"""
log(::Circle, ::Any...)
log(::Circle{â„}, p::Real, q::Real) = sym_rem(q - p)
function log(M::Circle{â„‚}, p::Number, q::Number)
    cosÎ¸ = complex_dot(p, q)
    if cosÎ¸ â‰ˆ -1  # appr. opposing points, return deterministic choice from set-valued log
        X = real(p) â‰ˆ 1 ? 1im : 1 + 0im
        X = X - complex_dot(p, X) * p
        X *= Ï€ / norm(X)
    else
        cosÎ¸ = cosÎ¸ > 1 ? one(cosÎ¸) : cosÎ¸
        Î¸ = acos(cosÎ¸)
        X = (q - cosÎ¸ * p) / usinc(Î¸)
    end
    return project_tangent(M, p, X)
end

log!(::Circle{â„}, X, p, q) = (X .= sym_rem(q - p))
function log!(M::Circle{â„‚}, X, p, q)
    cosÎ¸ = complex_dot(p, q)
    if cosÎ¸ â‰ˆ -1
        X .= sum(real.(p)) â‰ˆ 1 ? 1.0im : 1.0 + 0.0im
        X .= X - complex_dot(p, X) * p
        X .*= Ï€ / norm(X)
    else
        cosÎ¸ = cosÎ¸ > 1 ? one(cosÎ¸) : cosÎ¸
        Î¸ = acos(cosÎ¸)
        X .= (q - cosÎ¸ * p) / usinc(Î¸)
    end
    return project_tangent!(M, X, p, X)
end

@doc raw"""
    manifold_dimension(M::Circle)

Return the dimension of the [`Circle`](@ref) `M`,
i.e. $\dim(ğ•Š^1) = 1$.
"""
manifold_dimension(::Circle) = 1

@doc raw"""
    mean(M::Circle, x::AbstractVector[, w::AbstractWeights])

Compute the Riemannian [`mean`](@ref mean(M::Manifold, args...)) of `x` of points on the [`Circle`](@ref) $ğ•Š^1$,
which is computed with wrapped mean, i.e. the remainder of the mean modulo 2Ï€.
"""
mean(::Circle, ::Any)
mean(::Circle, x::Array{<:Real}; kwargs...) = sym_rem(sum(x))
mean(::Circle, x::Array{<:Real}, w::AbstractVector; kwargs...) = sym_rem(sum(w .* x))

@inline norm(::Circle, p, X) = sum(abs, X)

@doc raw"""
    project_point(M::Circle, p)

Project a point `p` onto the [`Circle`](@ref) `M`.
For the real-valued case this is the remainder with respect to modulus $2Ï€$.
For the complex-valued case the result is the projection of `p` onto the unit circle in the
complex plane.
"""
project_point(::Circle, ::Any)
project_point(::Circle{â„}, p::Real) = sym_rem(p)
project_point(::Circle{â„‚}, p::Number) = p / abs(p)

project_point!(::Circle{â„}, q, p) = copyto!(q, sym_rem(p))
project_point!(::Circle{â„‚}, q, p) = copyto!(q, p / sum(abs.(p)))

@doc raw"""
    project_tangent(M::Circle, p, X)

Project a value `X` onto the tangent space of the point `p` on the [`Circle`](@ref) `M`.

For the real-valued case this is just the identity.
For the complex valued case `X` is projected onto the line in the complex plane
that is parallel to the tangent to `p` on the unit circle and contains `0`.
"""
project_tangent(::Circle, ::Any, ::Any)
project_tangent(::Circle{â„}, p::Real, X::Real) = X
project_tangent(::Circle{â„‚}, p::Number, X::Number) = X - complex_dot(p, X) * p

project_tangent!(::Circle{â„}, Y, p, X) = (Y .= X)
project_tangent!(::Circle{â„‚}, Y, p, X) = (Y .= X - complex_dot(p, X) * p)

retract(M::Circle, p, q) = retract(M, p, q, ExponentialRetraction())
retract(M::Circle, p, q, m::ExponentialRetraction) = exp(M, p, q)

representation_size(::Circle) = ()

sharp(M::Circle, p::Number, Î¾::CoTFVector) = FVector(TangentSpace, Î¾.data)

sharp!(M::Circle, X::TFVector, p, Î¾::CoTFVector) = copyto!(X, Î¾)

show(io::IO, ::Circle{ğ”½}) where {ğ”½} = print(io, "Circle($(ğ”½))")

@doc raw"""
    sym_rem(x,[T=Ï€])

Compute symmetric remainder of `x` with respect to the interall 2*`T`, i.e.
`(x+T)%2T`, where the default for `T` is $Ï€$
"""
function sym_rem(x::N, T = Ï€) where {N<:Number}
    return (x â‰ˆ T ? convert(N, -T) : rem(x, convert(N, 2 * T), RoundNearest))
end
sym_rem(x, T = Ï€) where {N} = map(sym_rem, x, Ref(T))

@doc raw"""
    vector_transport_to(M::Circle, p, X, q, ::ParallelTransport)

Compute the parallel transport of `X` from the tangent space at `p` to the tangent space at
`q` on the [`Circle`](@ref) `M`.
For the real-valued case this results in the identity.
For the complex-valud case, the formula is the same as for the [`Sphere`](@ref)`(1)` in the
complex plane.
````math
\mathcal P_{qâ†p} X = X - \frac{âŸ¨\log_p q,XâŸ©_p}{d^2_{â„‚}(p,q)}
\bigl(\log_p q + \log_q p \bigr),
````
where [`log`](@ref) denotes the logarithmic map on `M`.
"""
vector_transport_to(::Circle, ::Any, ::Any, ::Any, ::ParallelTransport)
vector_transport_to(::Circle{â„}, p::Real, X::Real, q::Real, ::ParallelTransport) = X
function vector_transport_to(
    M::Circle{â„‚},
    p::Number,
    X::Number,
    q::Number,
    ::ParallelTransport,
)
    X_pq = log(M, p, q)
    Xnorm = norm(M, p, X_pq)
    Y = X
    if Xnorm > 0
        factor = 2 * complex_dot(X, q) / (abs(p + q)^2)
        Y -= factor .* (p + q)
    end
    return Y
end

vector_transport_to!(::Circle{â„}, Y, p, X, q, ::ParallelTransport) = (Y .= X)
function vector_transport_to!(M::Circle{â„‚}, Y, p, X, q, ::ParallelTransport)
    X_pq = log(M, p, q)
    Xnorm = norm(M, p, X_pq)
    Y .= X
    if Xnorm > 0
        factor = 2 * complex_dot(X, q) / (sum(abs.(p + q) .^ 2))
        Y .-= factor .* (p + q)
    end
    return Y
end

function vector_transport_direction(
    M::Circle,
    p::Number,
    X::Number,
    Y::Number,
    m::AbstractVectorTransportMethod,
)
    q = exp(M, p, Y)
    return vector_transport_to(M, p, X, q, m)
end

zero_tangent_vector(::Circle, p::Number) = zero(p)
zero_tangent_vector!(::Circle, X, p) = fill!(X, 0)
